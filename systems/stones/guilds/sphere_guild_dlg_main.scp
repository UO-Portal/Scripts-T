//****************************************************************************
// SPHERE by : Menasoft ©1997-2023
// www.sphereserver.net
//****************************************************************************
// FILE LAST UPDATED: 26.10.2023
//
VERSION=0.56T
	
[dialog d_Guild_Main]
5,25

IF (<TYPE>!=T_STONE_GUILD)
	RETURN 1
ELIF (<MEMBERFROMUID.<SRC.UID>.UID>!=<SRC.UID>)&&(!<SRC.ISGM>)
	RETURN 1
ENDIF
  LOCAL.CHARPRIV=<QVAL <SRC.ISGM> ? <DEF0.STONEPRIV_MASTER>:<MEMBERFROMUID.<SRC.UID>.PRIV>>
IF (<LOCAL.CHARPRIV>!=<DEF0.STONEPRIV_MEMBER>)&&(<LOCAL.CHARPRIV>!=<DEF0.STONEPRIV_MASTER>)
	RETURN 1
ENDIF


PAGE 0
RESIZEPIC 0 0 5054 550 400
RESIZEPIC 10 10 3000 530 380

DORIGIN 20 40

DHTMLGUMP +0 +0 220 35 0 0 Guild adı: <name>
IF (<MASTERUID>)
	LOCAL.MASTERNAME=<MASTERGENDERTITLE>: <QVAL (<EVAL STRLEN(<MASTERTITLE>)>) ? <MASTERTITLE> :><MASTER>
	DHTMLGUMP 275 15 250 20 0 0 <LOCAL.MASTERNAME>
ENDIF


dhtmlgump 55 50 100 20 0 0 Loyal to: 
local.loyalto=<memberfromuid.<src.uid>.loyalto>
dhtmlgump 55 70 470 20 0 0 <qval (!<local.loyalto>)||(<local.loyalto>==<src.uid>) ? yourself:<uid.<local.loyalto>.name>>
  button 20 50 4005 4007 1 0 1 

dhtmlgump 275 50 470 20 0 0 Display guild abbreviation: <abbrev>
dhtmlgump 275 70 90 20 0 0 Currently: <AbbreviationToggle>
button 240 50 4005 4007 1 0 2


BUTTON +0 100 4005 4007 1 0 3
DHTMLGUMP +30 100 150 30 0 0 Üye listesi

BUTTON +0 130 4005 4007 1 0 4
DHTMLGUMP +30 130 240 30 0 0 Üye al

IF (<MEMBER.COUNT <DEF0.STONEPRIV_CANDIDATE>>)
	BUTTON +0 160 4005 4007 1 0 5
	DHTMLGUMP +30 160 380 30 0 0 Üye aday listesi
ELSE
	BUTTON +0 160 4020 4020 0 0 0 
	DHTMLGUMP +30 160 320 30 0 0 Üye adayı yok.
ENDIF

BUTTON +0 220 4005 4007 1 0 6
DHTMLGUMP +30 220 240 30 0 0 Şartlar & Kurallar ve Tüzük
  
BUTTON +0 250 4005 4007 1 0 7
IF (<MASTERUID> == <SRC>)
	DHTMLGUMP +30 250 240 30 0 0 Yık
ELSE
	DHTMLGUMP +30 250 240 30 0 0 Ayrıl
ENDIF
  
BUTTON +0 280 4005 4007 1 0 8
DHTMLGUMP +30 280 240 30 0 0 Savaş durumu
  
IF (<SRC.ISGM>)||(<MASTERUID>==<SRC.UID>)
	BUTTON +0 310 4005 4007 1 0 9
	DHTMLGUMP +30 310 240 30 0 0 Guildmaster bölümü
ENDIF


[dialog d_Guild_Main Button]
ON=1 9
IF (<ARGN1> == 1)
	// DECLARE LOYALTO DIALOG
	SDIALOG D_GUILD_CHARLIST,0,<DEF0.GUILDDLG_LISTTYPE_LOYALITY>

ELIF (<ARGN1>==2)	
	// ABBREVIATION SWITCH
	TOGGLEABBREVIATION <SRC.UID>
	SDIALOG D_GUILD_MAIN,0,<UID>
	
ELIF (<ARGN1>==3)
	// Uye listesi
	SDIALOG D_GUILD_CHARLIST,0,<DEF0.GUILDDLG_LISTTYPE_ROSTER>
	
ELIF (<ARGN1>==4)	
	// Uye al
	SRC.TARGETF F_GUILDSYS_RECRUITGUY <UID>
	SRC.SYSMESSAGE @,,1,1 Üye almak istediğiniz kişiyi seçiniz.
  
ELIF (<ARGN1>==5)	// CANDIDATES LIST
  SDIALOG  D_GUILD_CHARLIST,0,<DEF0.GUILDDLG_LISTTYPE_CANDIDATES>
  
ELIF (<ARGN1>==6)	// GUILD CHARTER

  SDIALOG D_GUILD_VIEWCHARTER,0,<UID>
  
ELIF (<ARGN1>==7)	// RESIGN
  RESIGN
  
ELIF (<ARGN1>==8)	// ENEMIES
  SDIALOG D_GUILD_WARFARESTATUS,0,<UID>
  
ELIF (<ARGN1>==9)	// GUILDMASTER MENU
  IF (<SRC.ISGM>)||(<MASTERUID>==<SRC.UID>)
    SDIALOG D_GUILD_MASTER,0
  ENDIF
ENDIF
RETURN 1
	
[EOF]
