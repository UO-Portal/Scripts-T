


// SAYUA colour, talkmode, font, lang_id, text
[FUNCTION BANDAGE_DANGER_MSG]
// SAYUA 020,10,03,TRK * <ARGS> *
SAYUA 020,0,0,TRK * <ARGS> *
// MESSAGE @020,,1,1 * <ARGS> *

[FUNCTION BANDAGE_SUCCESS_MSG]
// SAYUA 048,10,03,TRK * <ARGS> *
SAYUA 048,0,0,TRK * <ARGS> *
// MESSAGE @048,,1,1 * <ARGS> *



[TYPEDEF T_BANDAGE]
ON=@DClick
IF (<TOPOBJ> != <SRC>)
	SRC.SYSMESSAGE @38,,1,1  Sadece üzerinizdeyken kullanabilirsiniz.
	RETURN 1
ELSEIF (<SRC.FINDID.I_HEALING_MEMORY>)
	SRC.SYSMESSAGE @38,,1,1  Şu an zaten tedavi aşamasındasınız.
	RETURN 1
// ELSEIF (<SRC.ISUSESKILL Magery>)
	// SRC.SYSMESSAGE @38,,1,1  Büyü yaparken bandaj kullanamazsınız.
	// RETURN 1
ELSEIF (<SRC.FLAGS>&STATF_DEAD|STATF_STONE|STATF_FREEZE)
	SRC.SYSMESSAGE @38,,1,1  Şu anki durumunuzda kullanamazsınız.
	RETURN 1
ELSEIF (<SRC.TAG0.HEALDELAY> > <SERV.TIME>)
	SRC.SYSMESSAGE @38,,1,1  Bandajları şu an kullanamazsınız.
	RETURN 1
ENDIF
TARGET
SRC.SYSMESSAGE <SERV.SKILL.17.PROMPT_MSG>
RETURN 1

ON=@TARGON_ITEM
SRC.SYSMESSAGE @38,,1,1  <def.msg_invalid_target_tr>
RETURN 1

on=@TARGON_GROUND
SRC.SYSMESSAGE @38,,1,1  <def.msg_invalid_target_tr>
RETURN 1

ON=@TARGON_CANCEL
SRC.SYSMESSAGE @,,1,1  Bandaj hedefi iptal edildi.

ON=@TARGON_CHAR
IF !(<SRC.RESTEST 1 I_BANDAGE>)
	SRC.SYSMESSAGE @38,,1,1  Bandajınız yok.
	RETURN 1
	
ELSEIF (<TOPOBJ> != <SRC>)
	SRC.SYSMESSAGE @38,,1,1  Sadece üzerinizdeyken kullanabilirsiniz.
	RETURN 1
	
// ELSEIF (<SRC.ISUSESKILL MAGERY>)
	// SRC.SYSMESSAGE @38,,1,1  Büyü yaparken bandaj kullanamazsınız.
	// RETURN 1
	
ELSEIF (<SRC.FINDID.I_HEALING_MEMORY>)
	SRC.SYSMESSAGE @38,,1,1  Şu an zaten tedavi aşamasındasınız.
	RETURN 1
	
ELSEIF (<SRC.FLAGS>&STATF_DEAD|STATF_STONE|STATF_FREEZE)
	SRC.SYSMESSAGE @38,,1,1  Şu anki durumunuzda kullanamazsınız.
	RETURN 1
	
ELSEIF (<SRC.TAG0.HEALDELAY> > <SERV.TIME>)
	SRC.SYSMESSAGE @38,,1,1  Bandajlari su an kullanamazsiniz.
	RETURN 1
	
ELSEIF (<EVAL <SRC.P.Z>+8> < <SRC.TARG.P.Z>) 
	SRC.SYSMESSAGE @38,,1,1  <src.targ.name> ile aynı yükselikte değilsin.
	RETURN 1
	
ELSEIF (<EVAL <SRC.P.Z>-8> > <SRC.TARG.P.Z>)
	SRC.SYSMESSAGE @38,,1,1  <src.targ.name> ile aynı yükselikte değilsin.
	RETURN 1
	
ELSEIF !(<SRC.CANSEELOS <SRC.TARG>>)
	SRC.SYSMESSAGE @38,,1,1  <SRC.TARG.NAME> isimli varlığı göremiyorsunuz.
	RETURN 1
	
ELSEIF (<SRC.DISTANCE <SRC.TARG>> > 4)
	SRC.SYSMESSAGE @38,,1,1  <SRC.TARG.NAME> yeterince yakınınızda değil.
	RETURN 1
	
ELSEIF !(<SRC.TARG.ISHEALABLECHAR>)
	SRC.SYSMESSAGE @38,,1,1  Bu varlığı iyileştiremezsiniz.
	RETURN 1
ENDIF

IF (<SRC.TARG.HITS> < <SRC.TARG.STR>)
	LOCAL.YARALI=1
ELSEIF (<SRC.TARG.FLAGS>&STATF_POISONED)
	LOCAL.YARALI=1
ELSEIF (<SRC.TARG.FINDID.I_BLEEDING>)
	LOCAL.YARALI=1
ELSEIF (<SRC.TARG.FINDID.I_BLEEDING_WOLF>)
	LOCAL.YARALI=1
ENDIF

IF (<LOCAL.YARALI> != 1)
	SRC.SYSMESSAGE @38,,1,1  <QVAL (<SRC.TARG> != <SRC>) ? <SRC.TARG.NAME> tedaviye ihtiyaç duymuyor. : Tedaviye ihtiyacınız yok.>
	IF (<SRC.TARG.HITS> > <SRC.TARG.STR>)
		SRC.TARG.HITS = <SRC.TARG.STR>
	ENDIF
	RETURN 1
ENDIF


// PLAYER İSE
IF (<SRC.TARG.ISPLAYER>) && !(<SRC.TARG.NPC>)

	IF (<SRC.TARG.UID> != <SRC>)
		// ATANIN GUİLDİ YOKSA VE ATILANIN VARSA
		IF !(<SRC.GUILD>) && (<SRC.TARG.GUILD>)  
			SRC.SYSMESSAGE @38,,1,1  Guild üyesi olmayanlar guild üyelerini tedavi edemezsiniz.
			RETURN 1
		ENDIF
		// ATANIN ve ATILANIN GUİLDİ VARSA VE ATILAN İLE AYNI DEĞİLSE
		IF (<SRC.GUILD>) && (<SRC.TARG.GUILD>)  
			IF (<SRC.TARG.GUILD.ALIGN> != <SRC.GUILD.ALIGN>)
				SRC.SYSMESSAGE @38,,1,1  <SRC.TARG.GUILD.ALIGNTYPE> guildi <SRC.GUILD.ALIGNTYPE> guild üyelerini tedavi edemezsiniz.
				RETURN 1
			ENDIF	
		ENDIF	
	ENDIF
		
	// PLAYER ÖLÜ İSE
	IF (<SRC.TARG.FLAGS>&STATF_DEAD)
		
		IF !(<SRC.RESTEST 5 i_bandage>)
			SRC.SYSMESSAGE @38,,1,1  Varlıkları canlandırabilmeniz için en az 5 bandajınız olmalı.
			RETURN 1
		ELSEIF (<SRC.TARG.REGION.FLAGS>&REGION_FLAG_HOUSE)
			SRC.SYSMESSAGE @38,,1,1  <SRC.TARG.NAME> ev içinde olduğu için hayata döndüremezsiniz.
			RETURN 1
		ELSEIF (<SRC.TARG.MEMORYFINDTYPE.MEMORY_TOWN>) && !(<SRC.MEMORYFINDTYPE.MEMORY_TOWN>)
			SRC.SYSMESSAGE @38,,1,1  Faction uyelerini iyilestiremezsiniz.
			RETURN 1
		ENDIF
		
		SRC.SAYUA 048,0,0,0 * <SRC.TARG.NAME> isimli oyuncuyu diriltir *
		SRC.TARG.SAYUA 048,0,0,0 * <SRC.NAME> sayesinde hayata doner *
		SRC.TARG.RESURRECT 2
		SRC.TARG.HITS = <QVAL (<SRC.TARG.STR> >= <SERV.HitPointPercentOnRez>)?<SERV.HitPointPercentOnRez>:1>
		SRC.TARG.EFFECT 3,0376A,1,16,0,0,0
		SRC.CONSUME 10 i_bandage
		SRC.SYSMESSAGE @044,,1,1 <SRC.TARG.NAME> isimli oyuncuyu dirilttin.

	// PLAYER CANLI İSE
	ELSE
		SERV.NEWITEM I_HEALING_MEMORY,1,<SRC>,1
		NEW.LINK <SRC.TARG>	
		SRC.SYSMESSAGE @044,,1,1 <QVAL (<SRC> == <SRC.TARG>)?Tedaviye başladın:<SRC.TARG.NAME> isimli oyuncuyu tedavi etmeye başladın>.
	ENDIF
	
// Player değile
ELSE
	SERV.NEWITEM I_HEALING_MEMORY,1,<SRC>,1
	NEW.LINK <SRC.TARG>	
	SRC.SYSMESSAGE @044,,1,1 <QVAL (<SRC> == <SRC.TARG>)?Tedaviye başladın:<SRC.TARG.NAME> isimli varlığı tedavi etmeye başladın>.
ENDIF

SRC.FACE <SRC.TARG>
RETURN 1




[ITEMDEF I_HEALING_MEMORY]
DEFNAME=I_HEALING_MEMORY
ID=I_MEMORY
TYPE=T_EQ_SCRIPT
NAME=Healing
ON=@CREATE
ATTR=ATTR_DECAY|ATTR_NEWBIE

ON=@EQUIP
SRC.EVENTS +E_HEALING
SRC.ACTION -1
SRC.SKILL FAIL
SRC.FREEZETIME_CUSTOM 30
SRC.ANIM 17
TIMER=5
// SRC.POTIONCOOLDOWN_OPENDLG <SERV.ITEMDEF.I_BANDAGE.ID>,50

ON=@UNEQUIP
SRC.EVENTS -E_HEALING
SRC.FREEZETIME_CUSTOM 0

ON=@TIMER
IF (<LINK.HITS> < <LINK.STR>)
	LOCAL.YARALI=1
ELSEIF (<LINK.FLAGS>&STATF_POISONED)
	LOCAL.YARALI=1
ELSEIF (<LINK.FINDID.I_BLEEDING>)
	LOCAL.YARALI=1
ELSEIF (<LINK.FINDID.I_BLEEDING_WOLF>)
	LOCAL.YARALI=1
ELSE
	LOCAL.YARALI=0
ENDIF

IF (<CONT.FLAGS>&STATF_STONE)
	CONT.SYSMESSAGE @38,,1,1  Şu anki durumunuzda kullanamazsınız.
	REMOVE
	RETURN 1
	
ELSEIF !(<CONT.RESTEST 1 I_BANDAGE>)
	CONT.SYSMESSAGE @38,,1,1  Bandajiniz yok.
	REMOVE
	RETURN 1
	
ELSEIF !(<LOCAL.YARALI>)
	CONT.SYSMESSAGE @38,,1,1  <QVAL (<LINK> != <CONT>) ? <LINK.NAME> tedaviye ihtiyaç duymuyor. : Tedaviye ihtiyacınız yok.>
	REMOVE
	RETURN 1

ELSEIF (<LINK.FLAGS>&STATF_DEAD)
	CONT.SYSMESSAGE @38,,1,1  <link.name> tedavi esnasinda hayatini kaybetti.
	REMOVE
	RETURN 1
	
ELSEIF (<EVAL <CONT.P.Z>+8> < <LINK.P.Z>) 
	CONT.SYSMESSAGE @38,,1,1  <LINK.name> ile aynı yükselikte değilsin.
	REMOVE
	RETURN 1
	
ELSEIF (<EVAL <CONT.P.Z>-8> > <LINK.P.Z>)
	CONT.SYSMESSAGE @38,,1,1  <LINK.name> ile aynı yükselikte değilsin.
	REMOVE
	RETURN 1
	
ELSEIF !(<LINK.ISHEALABLECHAR>)
	CONT.SYSMESSAGE @38,,1,1  Bu varlığı iyileştiremezsiniz.
	REMOVE
	RETURN 1
ENDIF

IF (<LINK.FLAGS>&STATF_POISONED) // ZEHİR İYİLEŞTİR

	IF !(RAND(5) == 1)
		// Success
		LINK.CURE 100.0
		LINK.EFFECT 3,0376A,2,10,1		
		CONT.Heal_Poison_SuccessMsg <LINK>
	ELSE
		// Fail
		CONT.Heal_Poison_FailMsg <LINK>
	ENDIF
	
	
ELSEIF (<LINK.FINDID.I_BLEEDING>) // YARA İYİLEŞTİR

	IF !(RAND(5) == 1)
		// Success
		LINK.FINDID.I_BLEEDING.REMOVE
		LINK.EFFECT 3,0376A,2,10,1	
		CONT.Heal_Bleeding_SuccessMsg <LINK>
	ELSE
		// Fail
		CONT.Heal_Bleeding_FailMsg <LINK>
	ENDIF

	
ELSEIF (<LINK.FINDID.I_BLEEDING_WOLF>) // WOLF YARA İYİLEŞTİR

	IF !(RAND(5) == 1)
		// Success
		LINK.FINDID.I_BLEEDING_WOLF.REMOVE
		LINK.EFFECT 3,0376A,2,10,1	
		CONT.Heal_Bleeding_SuccessMsg <LINK>
	ELSE
		// Fail
		CONT.Heal_Bleeding_FailMsg <LINK>
	ENDIF
	
ELSE // TEDAVİ ET

	// LOCAL.OLDHITS = <CONT.HITS>
	// LOCAL.HEAL1 <EVAL (1000 / <R20,50>) + (1000 / 50)>
	// LOCAL.HEAL2 <EVAL (<LOCAL.HEAL1> + <R3,10>)>	
	LOCAL.HEAL2 <R35,40>
	
	LINK.HITS +=<LOCAL.HEAL2>
	IF (<LINK.HITS> > <LINK.STR>)
		LINK.HITS = <LINK.STR>
	ENDIF
	
	CONT.Heal_SuccessMsg <LINK>
	LINK.EFFECT 3,0376A,2,10,1
	
ENDIF

CONT.TAG0.HEALDELAY = <EVAL <SERV.TIME> + <QVAL (<CONT.DEX> > 35)?80:100>>
CONT.CONSUME 1 I_BANDAGE
REMOVE
RETURN 1




[events e_healing]
ON=@LOGOUT
IF (<SRC.FINDID.I_HEALING_MEMORY>)
	SRC.FINDID.I_HEALING_MEMORY.REMOVE
ENDIF

// ON=@SPELLSELECT
// IF (<SRC.FINDID.I_HEALING_MEMORY>)
	// SRC.SYSMESSAGE @38,,1,1  Bandaj basarken büyü atamazsınız.
	// RETURN 1
// ENDIF

On=@GetHit
// IF (RAND(4) == 1)
IF (RAND(5) == 1)
	// SKILL FAIL
	IF (<FINDID.I_HEALING_MEMORY>)
		FINDID.I_HEALING_MEMORY.REMOVE
	ENDIF
	TAG0.HEALDELAY=
	
	BANDAGE_DANGER_MSG bandajları yırtılır
ENDIF

on=@SkillStart
IF ((<ARGN1> == 25) || (<ARGN1> == 49))
	IF (<FINDID.I_HEALING_MEMORY>)
		FINDID.I_HEALING_MEMORY.REMOVE
	ENDIF
	BANDAGE_DANGER_MSG bandajları yırtılır
	TAG0.HEALDELAY=
ENDIF






// Messages functions


// Heal
[FUNCTION Heal_SuccessMsg]
REF1=<ARGS>
IF (<UID> == <REF1>)
	BANDAGE_SUCCESS_MSG kendini tedavi eder
ELSE
	BANDAGE_SUCCESS_MSG <REF1.name> isimli varlığı iyileştirir
	// REF1.TAG0.HEALDELAY=
	REF1.BANDAGE_SUCCESS_MSG <NAME> tarafından iyileştirilir
ENDIF






// Bleeding

[FUNCTION Heal_Bleeding_SuccessMsg]
REF1=<ARGS>
IF (<UID> == <REF1>)
	BANDAGE_SUCCESS_MSG yarayı tedavi eder
ELSE
	BANDAGE_SUCCESS_MSG <REF1.name> isimli varlığın yarasını iyileştirir
	REF1.BANDAGE_SUCCESS_MSG <NAME> tarafından yarası iyileştirilir
ENDIF


[FUNCTION Heal_Bleeding_FailMsg]
REF1=<ARGS>
IF (<UID> == <REF1>)
	BANDAGE_DANGER_MSG yarayı iyileştiremez
ELSE
	BANDAGE_DANGER_MSG <REF1.NAME> üzerindeki yarayı iyileştiremez
ENDIF






// Poison
[FUNCTION Heal_Poison_SuccessMsg]
REF1=<ARGS>
IF (<UID> == <REF1>)
	BANDAGE_SUCCESS_MSG zehiri iyileştirir
ELSE
	BANDAGE_SUCCESS_MSG <REF1.name> isimli varlığın zehrini iyileştirir
	REF1.BANDAGE_SUCCESS_MSG <NAME> tarafından zehri iyileştirilir
ENDIF


[FUNCTION Heal_Poison_FailMsg]
REF1=<ARGS>
IF (<UID> == <REF1>)
	BANDAGE_DANGER_MSG zehiri iyileştiremez
ELSE
	BANDAGE_DANGER_MSG <REF1.NAME> üzerindeki zehiri iyileştiremez
ENDIF




[EOF]





