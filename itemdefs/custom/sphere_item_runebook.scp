

[FUNCTION RUNEBOOK_TABLE_CREATE]
IF (<DEF.SQLITE_RUNEBOOKS>)
	IF !(<TAG0.LINK>)
		LDB.CONNECT "db/runebooks.db"
		IF (<LDB.CONNECTED>)
			LDB.EXECUTE CREATE TABLE IF NOT EXISTS 'Runebook_<UID>' ('id' INTEGER PRIMARY KEY AUTOINCREMENT NOT NULL, 'rune_uid' TEXT, 'rune_name' TEXT, 'rune_morep' TEXT, 'rune_px' INT, 'rune_py' INT, 'rune_pz' INT, 'rune_pm' INT, 'region_name' TEXT, 'region_text' TEXT, 'region_flags' TEXT, 'page' INT, 'ordering' INT, 'created_at' TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP, 'updated_at' TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP)
			LDB.EXECUTE BEGIN
			FOR 1 1000
				LDB.Query "SELECT rowid, id FROM 'Runebook_<UID>' WHERE id='<dlocal._for>'"
				IF !(<LDB.ROW.NUMROWS>)
					LDB.EXECUTE INSERT INTO 'Runebook_<UID>' (rune_uid,ordering) VALUES (NULL,'0')
				ENDIF	
			ENDFOR
			LDB.EXECUTE COMMIT
		LDB.CLOSE
		ENDIF
	ENDIF
ENDIF



[ITEMDEF I_RUNEBOOK_COPY]
DEFNAME=I_RUNEBOOK_COPY
ID=i_book_lg
TYPE=T_SCRIPT
WEIGHT=0
VALUE=85000
VALUESP=0
VALUECP=0
NAME=Runebook Copier
RESDISPDNHUE=1301
RESOURCES=
SKILLMAKE=
TAG0.URL=
CATEGORY=
SUBSECTION=
DESCRIPTION=

ON=@Create
COLOR=<SERV.ITEMDEF.<BASEID>.RESDISPDNHUE>

ON=@CLICK
MESSAGE @08A1,,1 <NAME>
RETURN 1

ON=@DClick
IF !(<SRC.ISINPACK <UID>>)
	SRC.SYSMESSAGE @38,,1,1 <DEF.UNPACKMSG>
	RETURN 1
ENDIF
TARGET 
SRC.SYSMESSAGE @,,1,1 Kopyalamak istediğiniz Runebooku seçiniz.
RETURN 1

ON=@TARGON_ITEM
IF !(<SRC.TARG.ISRUNEBOOK>)
	SRC.SYSMESSAGE @38,,1,1 Bu bir Runebook değil.
	RETURN 1
ELSEIF !(<SRC.ISINPACK <UID>>)
	SRC.SYSMESSAGE @38,,1,1 Çantanızda Runebook Kopyalama Kitabı bulunmuyor.
	RETURN 1
ELSEIF !(<SRC.ISINPACK <SRC.TARG.UID>>)
	SRC.SYSMESSAGE @38,,1,1 Bu Runebook çantanızda değil.
	RETURN 1
ELSEIF (<SRC.TARG.TAG0.COPY>)
	SRC.SYSMESSAGE @38,,1,1 Bu Runebook zaten bir Kopya.
	RETURN 1
ELSEIF (<SRC.TARG.TAG0.LINK>)
	SRC.SYSMESSAGE @38,,1,1 Bu Runebook linklenmiş.
	RETURN 1
ENDIF
IF (<DEF.SQLITE_RUNEBOOKS>)
	LDB.CONNECT "db/runebooks.db"
	IF (<LDB.CONNECTED>)
		LDB.Query "SELECT rowid, id FROM 'Runebook_<SRC.TARG.UID>' WHERE rune_uid IS NOT NULL"
		
		IF !(<LDB.ROW.NUMROWS>)
			SRC.SYSMESSAGE @38,,1,1 Bu runebook boş.
			RETURN 1
		ENDIF	

		SERV.NEWDUPE <SRC.TARG.UID>
		NEW.BOUNCE
		REF1=<NEW.UID>
		
		NEW.TAG0.COPY=<SRC.TARG.UID>
		NEW.TAG0.NAME=<SRC.TARG.TAG0.NAME>
		NEW.TAG0.RUNEBOOK_LASTPAGE=<SRC.TARG.TAG0.RUNEBOOK_LASTPAGE>

		LDB.EXECUTE CREATE TABLE IF NOT EXISTS 'Runebook_<NEW.UID>' ('id' INTEGER PRIMARY KEY AUTOINCREMENT NOT NULL, 'rune_uid' TEXT, 'rune_name' TEXT, 'rune_morep' TEXT, 'rune_px' INT, 'rune_py' INT, 'rune_pz' INT, 'rune_pm' INT, 'region_name' TEXT, 'region_text' TEXT, 'region_flags' TEXT, 'page' INT, 'ordering' INT, 'created_at' TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP, 'updated_at' TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP)
		LDB.EXECUTE BEGIN
		FOR 1 1000
			LDB.Query "SELECT rowid, id FROM 'Runebook_<NEW.UID>' WHERE id='<dlocal._for>'"
			IF !(<LDB.ROW.NUMROWS>)
				LDB.EXECUTE INSERT INTO 'Runebook_<NEW.UID>' (rune_uid,ordering) VALUES (NULL,'0')
			ENDIF		
		ENDFOR
		LDB.EXECUTE COMMIT
	LDB.CLOSE
	ENDIF
ENDIF


SRC.SOUND=79
SRC.SYSMESSAGE @044,,1,1 Runebook başarıyla kopyalandı.
REMOVE
RETURN 1

ON=@TARGON_CHAR
SRC.SYSMESSAGE @38,,1,1 <def.msg_invalid_target_tr>
RETURN 1

ON=@TARGON_GROUND
SRC.SYSMESSAGE @38,,1,1 <def.msg_invalid_target_tr>
RETURN 1

ON=@TARGON_CANCEL
SRC.SYSMESSAGE @38,,1,1 Runebook kopyalamaktan vazgeçtin.




[ITEMDEF I_RUNEBOOK]
DEFNAME=I_RUNEBOOK
ID=I_SPELLBOOK_RUNEBOOK
TYPE=t_container
TDATA2=03d
WEIGHT=0.0
NAME=Runebook
RESOURCES=
SKILLMAKE=
VALUECP=0
VALUESP=0
VALUE=0
RESDISPDNHUE=07d4
CATEGORY=
SUBSECTION=
DESCRIPTION=

ON=@Create
COLOR=<SERV.ITEMDEF.<BASEID>.RESDISPDNHUE>
ATTR=ATTR_NEWBIE|ATTR_MAGIC
TYPE=<SERV.ITEMDEF.<BASEID>.TYPE>

ON=@CLICK
DISPID=<SERV.ITEMDEF.<BASEID>.ID>
ATTR |= ATTR_NEWBIE
ATTR |= ATTR_MAGIC
TYPE=<SERV.ITEMDEF.<BASEID>.TYPE>
UPDATE

IF (<TAG0.LINK>)
	REF1=<OWNEDBY>
ELSE
	REF1=<UID>
ENDIF
IF (<DEF.SQLITE_RUNEBOOKS>)
	LDB.CONNECT "db/runebooks.db"
	IF (<LDB.CONNECTED>)
		LDB.Query "SELECT rowid, id FROM 'Runebook_<REF1>' WHERE rune_uid IS NOT NULL"
			IF (<DLDB.ROW.NUMROWS>)
				MESSAGE (<dLDB.ROW.NUMROWS>/1000 Rune)
			ENDIF
	LDB.CLOSE
	ENDIF
ENDIF

IF (<ATTR>&ATTR_NEWBIE)
	MESSAGE <defmsg.TOOLTIP_TAG_NEWBIE>
ENDIF

IF !(<ISBLANKSTRING <REF1.TAG0.NAME>>)
	LOCAL.NAME <REF1.NAME> (<REF1.TAG0.NAME>)
ELSE
	LOCAL.NAME <REF1.NAME>
ENDIF

IF (<TAG0.COPY>)
	LOCAL.NAME .= [Kopya]
ELSEIF (<TAG0.LINK>)
	LOCAL.NAME .= [Link]
ENDIF

MESSAGE @,,1,1 <LOCAL.NAME>
RETURN 1


ON=@DClick
DISPID=<SERV.ITEMDEF.<BASEID>.ID>
ATTR |= ATTR_NEWBIE
ATTR |= ATTR_MAGIC
TYPE=<SERV.ITEMDEF.<BASEID>.TYPE>
UPDATE

IF (<SRC.CTAG0.RunebookDclick.DELAY> > <SERV.TIME>)
	RETURN 1
ENDIF
IF !(<TAG0.LINK>)
	runebook_Table_Create
ENDIF
IF (<CONT>) && (<TOPOBJ.UID> != <SRC.UID>) && (<SRC.ACCOUNT.PLEVEL> < 6)
	SRC.SYSMESSAGE @38,,1,1 Sadece yerdeki veya kendi çantanızdaki Runebookları açabilirsiniz.
	RETURN 1
ELSEIF (<SRC.ISJAIL>)
	SRC.SYSMESSAGE @38,,1,1 Hapisteyken bunu kullanamazsınız.
	RETURN 1
ELSEIF (<SRC.REGION.FLAGS>&REGION_FLAG_SHIP)
    SRC.SYSMESSAGE @38,,1,1 Gemi içinde bunu kullanamazsınız.
	RETURN 1
// ELSEIF (<SRC.ISHELPSTUCK>)
	// SRC.SYSMESSAGE @38,,1,1 Stuck esnasında bunu kullanamazsınız.
	// RETURN 1
ELSEIF (<SRC.REGION.FLAGS>&REGION_FLAG_HOUSE)
    SRC.SYSMESSAGE @38,,1,1 Ev içinde bunu kullanamazsınız.
    RETURN 1
ELSEIF (<SRC.ISUSESKILL Magery>)
	SRC.SYSMESSAGE @38,,1,1 Büyü yaparken runebook kullanamazsınız.
	RETURN 1
ELSEIF (<SRC.ISUSESKILL Necromancy>)
	SRC.SYSMESSAGE @38,,1,1 Büyü yaparken runebook kullanamazsınız.
	RETURN 1
ENDIF
SRC.SOUND=85
SRC.CTAG0.RUNEBOOK_LAST=<QVAL (<TAG0.LINK>)?<TAG0.TABLE>:Runebook_<UID>>
SRC.CTAG0.RUNEBOOK_FROM=<QVAL (<TAG0.LINK>)?<TAG0.TABLE>:Runebook_<UID>>
SRC.CTAG0.RUNEBOOK_WHERE="id"
SRC.CTAG0.RUNEBOOK_ORDER="created_at ASC"
SRC.CTAG0.RunebookDclick.Delay=<EVAL <SERV.TIME>+10>
DIALOGCLOSE D_RUNEBOOK
DIALOG D_RUNEBOOK,<QVAL (<TAG0.RUNEBOOK_LASTPAGE>)?<DTAG0.RUNEBOOK_LASTPAGE>:1>

IF (<SRC.ISGM>)
    RETURN 0
ELSE
    RETURN 1
ENDIF


ON=@DROPON_GROUND
IF (<SRC.ISUSESKILL Magery>)
	SRC.SKILL FAIL
	SRC.SYSMESSAGE @38,,1,1 Runebook kullanımı esnasında Runebooku yere attığınız için işlem başarısız oldu.
ENDIF

ON=@TARGON_CHAR
SRC.SYSMESSAGE @38,,1,1 Hatali secim.
RETURN 1

ON=@DROPON_SELF
IF !(<SRC.ISINPACK <UID>>)
	SRC.SYSMESSAGE @38,,1,1 <NAME>: <defmsg.item_unpack>
	RETURN 1
ENDIF
// Rune ekle
IF ((<ARGO.BASEID> == I_RUNE_MARKER) || (<ARGO.TYPE> == T_RUNE))
	Runebook_Add_Rune <ARGO.UID>
ELSE
	SRC.SYSMESSAGE @38,,1,1 Bu eşyanın kitap ile bir ilgisi yok.
ENDIF
RETURN 1


ON=@TARGON_ITEM
/// ADD RUNE
IF (<MORE2> == 2)
	IF !(<SRC.TARG.TYPE> == T_RUNE)
		SRC.SYSMESSAGE @38,,1,1 Bu bir rune değil.
		MORE2=2
		TARGET
		RETURN 1
	ELSEIF (<SRC.TARG.MOREX>==0)
		SRC.SYSMESSAGE @38,,1,1 Bu rune boş. Bunu kullanamazsınız.
		MORE2=2
		TARGET
		RETURN 1
	ENDIF
	Runebook_Add_Rune <SRC.TARG.UID>
ENDIF
RETURN 1


ON=@TIMER
RETURN 1


ON=@DESTROY
IF !(<TAG0.LINK>)
	LDB.CONNECT "db/runebooks.db"
		IF (<LDB.CONNECTED>)
			LDB.EXECUTE DROP TABLE IF EXISTS 'Runebook_<UID>'
		ENDIF
	LDB.CLOSE
ENDIF



[FUNCTION RUNEBOOK_ADD_RUNE]
REF1=<ARGS>   // Rune
// UID = RUNEBOOK

IF !(<REF1>) || !(<UID>)
	SRC.SYSMESSAGE @38,,1,1 Bir hata oluştu.
	RETURN 1
ELSEIF !(<REF1.ISITEM>) || !(<ISITEM>)
	SRC.SYSMESSAGE @38,,1,1 Bir hata oluştu.
	RETURN 1
ELSEIF !(<REF1.TYPE> == T_RUNE)
	SRC.SYSMESSAGE @38,,1,1 Bu bir rune değil.
	RETURN 1
ELSEIF !(<ISRUNEBOOK>)
	SRC.SYSMESSAGE @38,,1,1 Bu bir Runebook değil.
	RETURN 1
ELSEIF (<TAG0.LINK>)
	SRC.SYSMESSAGE @38,,1,1 Bu başka bir Runebooka linklendiği için rune ekleyemezsiniz.
	RETURN 1
ELSEIF (<REF1.MOREX> <= 0)
	SRC.SYSMESSAGE @38,,1,1 Bu rune boş. Bunu kullanamazsınız.
	RETURN 1
ELSEIF !(<SRC.ISINPACK <REF1>>)
	SRC.SYSMESSAGE @38,,1,1 Ekleyeceğiniz şey çantanızda olmalı.
	RETURN 1
ENDIF

Runebook_Table_Create

IF !(<TAG0.RUNEBOOK_LASTPAGE>)
	TAG0.RUNEBOOK_LASTPAGE=1
ENDIF
	
LDB.CONNECT "db/runebooks.db"
IF (<LDB.CONNECTED>)

	LDB.Query "SELECT * FROM 'Runebook_<UID>' WHERE rune_uid IS NULL"
	IF (<LDB.ROW.NUMROWS>)
		LDB.EXECUTE "UPDATE 'Runebook_<UID>' SET rune_uid="<REF1>", rune_name="<REF1.NAME>", rune_morep="<REF1.MOREP>", rune_px="<REF1.MOREX>", rune_py="<REF1.MOREY>", rune_pz="<REF1.MOREZ>", rune_pm="<REF1.MOREM>", region_name="<STRING_REPLACER <SERV.MAP(<REF1.MOREX>,<REF1.MOREY>,<REF1.MOREZ>,<REF1.MOREM>).REGION.NAME>>", region_text="<STRING_REPLACER <SERV.MAP(<REF1.MOREX>,<REF1.MOREY>,<REF1.MOREZ>,<REF1.MOREM>).REGION.NAME>>", region_flags="<SERV.MAP(<REF1.MOREX>,<REF1.MOREY>,<REF1.MOREZ>,<REF1.MOREM>).REGION.FLAGS>", page="<TAG0.RUNEBOOK_LASTPAGE>", ordering="0" WHERE id='<LDB.ROW.0.id>'"
		//LDB.QUERY INSERT INTO 'Runebook_<UID>' (rune_uid, rune_name, rune_morep, rune_px, rune_py, rune_pz, rune_pm, region_name, region_text, region_flags, page, ordering) VALUES ("<REF1>","<REF1.NAME>","<REF1.MOREP>","<REF1.MOREX>","<REF1.MOREY>","<REF1.MOREZ>","<REF1.MOREM>","<STRING_REPLACER <SERV.MAP(<REF1.MOREX>,<REF1.MOREY>,<REF1.MOREZ>,<REF1.MOREM>).REGION.NAME>>","<STRING_REPLACER <SERV.MAP(<REF1.MOREX>,<REF1.MOREY>,<REF1.MOREZ>,<REF1.MOREM>).REGION.NAME>>","<SERV.MAP(<REF1.MOREX>,<REF1.MOREY>,<REF1.MOREZ>,<REF1.MOREM>).REGION.FLAGS>","<TAG0.RUNEBOOK_LASTPAGE>","0")
	ENDIF
	SRC.SYSMESSAGE @,,1,1 Runeyi kitaba eklediniz.
	SRC.SOUND=79
	IF (<REF1.ISITEM>) && (<REF1.TYPE> == T_RUNE)
		// REF1.TAG0.TIME=<SERV.TIMEHIRES>
		REF1.CONT=<UID>
		REF1.UPDATEX
		SRC.DCLICK <UID>
	ENDIF
ENDIF
LDB.CLOSE

RETURN 1





[DIALOG D_RUNEBOOK_LINKS]
0,0
LOCAL.MAIN_X=<EVAL <SRC.CTAG0.RUNEBOOK_DEFAULT_DIALOG_MAIN_X>-100>
LOCAL.MAIN_Y=<EVAL <SRC.CTAG0.RUNEBOOK_DEFAULT_DIALOG_MAIN_Y>+60>

DORIGIN <DLOCAL.MAIN_X> <DLOCAL.MAIN_Y>
GUMPPIC +0 +0 02b2f

CLEARTAGS RUNEBOOK_LINKS

REF1=<UID>
FORINSTANCES I_RUNEBOOK
REF2=<UID>
	IF (<REF2.TAG0.LINK>)
		IF (<REF2.OWNEDBY> == <REF1>)
			REF1.TAG0.RUNEBOOK_LINKS ++
			REF1.TAG0.RUNEBOOK_LINK.<REF1.TAG0.RUNEBOOK_LINKS> <REF2>
		ENDIF
	ENDIF
ENDFOR

DHTMLGUMP +65 +40 130 60 0 0 <DEF.CENTER>Bu runebook <QVAL (<DTAG0.RUNEBOOK_LINKS>)?toplam <DTAG0.RUNEBOOK_LINKS> kere linklenmiş:hiç linklenmemiş>.


PAGE 1
LOCAL.AUMENTAR=0
LOCAL.PAGE=1
LOCAL.X=<EVAL <LOCAL.MAIN_X>+60>
LOCAL.Y=<EVAL <LOCAL.MAIN_Y>+60>

IF (<DTAG0.RUNEBOOK_LINKS>)
	FOR 1 <DTAG0.RUNEBOOK_LINKS>

	IF (<LOCAL.AUMENTAR> >= 10)
		LOCAL.AUMENTAR=0
		BUTTON <EVAL <LOCAL.MAIN_X>+320> <EVAL <LOCAL.MAIN_Y>+310> 0C33B 0C33C 0 <EVAL <LOCAL.PAGE>+1> 0
		LOCAL.PAGE += 1
		LOCAL.X=<EVAL <LOCAL.MAIN_X>+60>
		LOCAL.Y=<EVAL <LOCAL.MAIN_Y>+60>
		PAGE <EVAL <LOCAL.PAGE>>
		BUTTON <EVAL <LOCAL.MAIN_X>+70> <EVAL <LOCAL.MAIN_Y>+310> 0C338 0C339 0 <EVAL <LOCAL.PAGE>-1> 0
	ENDIF

	REF100=<TAG0.RUNEBOOK_LINK.<DLOCAL._FOR>>

	IF (<REF100>)
		LOCAL.Y +=25

		BUTTON <EVAL (<DLOCAL.X>)> <EVAL (<DLOCAL.Y>)> 0BD 0BE 1 0 <DLOCAL._FOR>
		DCROPPEDTEXT <EVAL (<DLOCAL.X>)+25> <EVAL (<DLOCAL.Y>)+3> 100 25 <DEF.SMSG_WHITE_COLOR> #<DLOCAL._FOR> <REF100.TOPOBJ.NAME>
		LOCAL.AUMENTAR += 1

		IF (<LOCAL.Y> > 560)
			LOCAL.X +=160
			LOCAL.Y=<EVAL <LOCAL.MAIN_Y>+10>
			
		ENDIF
	
	ENDIF
	
	ENDFOR


ENDIF


[DIALOG D_RUNEBOOK_LINKS BUTTON]
ON=1 999
REF100=<TAG0.RUNEBOOK_LINK.<DARGN>>

IF (<REF100>)
	IF (<REF100>) && (<REF100.ISITEM>)
		IF (<REF100.ISRUNEBOOK>) && (<REF100.OWNEDBY> == <UID>)		
			SRC.SYSMESSAGE @030,,1,1 <REF100.TOPOBJ.NAME> isimli oyuncudaki kopya runebook silindi.
			REF200=<REF100.TOPOBJ>
			IF (<REF200>)
				IF (<REF200>)
					REF200.NOTICE <DEF.BFONT_NOTICE_DRED>Sahibinin <REF100.TOPOBJ.NAME> olan <REF100.NAME> sahibi tarafindan silindi.
				ENDIF
			ENDIF
			REF100.REMOVE
		ELSE
			SRC.SYSMESSAGE @38,,1,1 Bir hata oluştu!
		ENDIF
	ENDIF
ENDIF







[DIALOG D_RUNEBOOK]
0,0
LOCAL.MAIN_X <R300,500>
LOCAL.MAIN_Y <R200,300>
SRC.CTAG0.RUNEBOOK_DEFAULT_DIALOG_MAIN_X=<LOCAL.MAIN_X>
SRC.CTAG0.RUNEBOOK_DEFAULT_DIALOG_MAIN_Y=<LOCAL.MAIN_Y>

DORIGIN <DLOCAL.MAIN_X> <DLOCAL.MAIN_Y>

IF (<COLOR> != <SERV.ITEMDEF.<BASEID>.RESDISPDNHUE>)
LOCAL.GUMPCOLOR=0
ELSE
LOCAL.GUMPCOLOR=0
ENDIF
GUMPPIC +0 +0 02b2f <LOCAL.GUMPCOLOR>

GUMPPIC +55 +30 57 0
GUMPPICTILED +65 +30 100 15 58
GUMPPIC +160 +30 59 0

GUMPPIC +210 +30 57 0
GUMPPICTILED +235 +30 100 15 58
GUMPPIC +320 +30 59 0

IF (<TAG0.RUNEBOOK_LASTPAGE> != 1)
BUTTON +70 +175 08B1 08B1 1 0 10
ENDIF
IF (<TAG0.RUNEBOOK_LASTPAGE> != 2)
BUTTON +100 +175 08B2 08B2 1 0 11
ENDIF
IF (<TAG0.RUNEBOOK_LASTPAGE> != 3)
BUTTON +135 +175 08B3 08B3 1 0 12
ENDIF
IF (<TAG0.RUNEBOOK_LASTPAGE> != 4)
BUTTON +165 +175 08B4 08B4 1 0 13
ENDIF

IF (<TAG0.RUNEBOOK_LASTPAGE> != 5)
BUTTON +220 +175 08B5 08B5 1 0 14
ENDIF
IF (<TAG0.RUNEBOOK_LASTPAGE> != 6)
BUTTON +255 +175 08B6 08B6 1 0 15
ENDIF
IF (<TAG0.RUNEBOOK_LASTPAGE> != 7)
BUTTON +290 +175 08B7 08B7 1 0 16
ENDIF
IF (<TAG0.RUNEBOOK_LASTPAGE> != 8)
BUTTON +320 +175 08B8 08B8 1 0 17
ENDIF

gumppic <EVAL <DLOCAL.MAIN_X>+180> <EVAL <DLOCAL.MAIN_Y>+210> 063a
DTEXTENTRYLIMITED <EVAL <DLOCAL.MAIN_X>+197> <EVAL <DLOCAL.MAIN_Y>+215> 30 25 <DEF.SMSG_SUCCESS_COLOR> 1 3 <DTAG0.RUNEBOOK_LASTPAGE> 

BUTTON +193 +187 0659 065a 1 0 6 // go page

BUTTON +320 +8 089e 089e 1 0 3 // ++ page

BUTTON +50 +8 089d 089d 1 0 4 // -- page

IF !(<TAG0.LINK>)
// LinkList Button
BUTTON +145 +18 2103 2104 1 0 5
DHTMLGUMP +157 +15 100 25 0 0 Links

//Runebook İsim değiştir
BUTTON +85 +18 2103 2104 1 0 1
IF !(<ISBLANKSTRING <TAG0.NAME>>)
	DHTMLGUMP +95 +15 50 25 0 0 <TAG0.NAME>
ELSE
	DHTMLGUMP +95 +15 50 25 0 0 İsimsiz
ENDIF

//RUNE EKLE
BUTTON +213 +13 037 038 1 0 2
DHTMLGUMP +228 +13 110 25 0 0 <DEF.SMALL>Rune

ENDIF

LOCAL.X = 350
LOCAL.Y = 260

LDB.CONNECT "db/runebooks.db"
IF (<LDB.CONNECTED>)
	LDB.Query "SELECT * FROM '<SRC.CTAG0.RUNEBOOK_FROM>' WHERE <SRC.CTAG0.RUNEBOOK_WHERE> LIMIT <TAG0.RUNEBOOK_PAGE_INDEX> ,18"
	IF (<lDB.ROW.NUMROWS>)
		WHILE <lDB.ROW.<DLOCAL._WHILE>.id>
			IF !(<ISEMPTY <lDB.ROW.<DLOCAL._WHILE>.id>>)
				
				IF !(<TAG0.LINK>)
					BUTTON <EVAL (<DLOCAL.X>+<EVAL <LOCAL.MAIN_X>>)-180> <EVAL (<DLOCAL.Y>+<EVAL <LOCAL.MAIN_Y>>)-215> 2103 2104 1 0 <EVAL <dlDB.ROW.<DLOCAL._WHILE>.id>+30000> // rename
					BUTTON <EVAL (<DLOCAL.X>+<EVAL <LOCAL.MAIN_X>>)-170> <EVAL (<DLOCAL.Y>+<EVAL <LOCAL.MAIN_Y>>)-215> 2103 2104 1 0 <EVAL <dlDB.ROW.<DLOCAL._WHILE>.id>+20000> // cikart
				ENDIF
				
				BUTTON <EVAL (<DLOCAL.X>+<EVAL <LOCAL.MAIN_X>>)-290> <EVAL (<DLOCAL.Y>+<EVAL <LOCAL.MAIN_Y>>)-215> 2103 2104 1 0 <EVAL <dlDB.ROW.<DLOCAL._WHILE>.id>+10000> // gate
				BUTTON <EVAL (<DLOCAL.X>+<EVAL <LOCAL.MAIN_X>>)-300> <EVAL (<DLOCAL.Y>+<EVAL <LOCAL.MAIN_Y>>)-215> 2103 2104 1 0 <EVAL <dlDB.ROW.<DLOCAL._WHILE>.id>+100> // recall

				LOCAL.P=<lDB.ROW.<DLOCAL._WHILE>.rune_px>,<lDB.ROW.<DLOCAL._WHILE>.rune_py>,<lDB.ROW.<DLOCAL._WHILE>.rune_pz>,<lDB.ROW.<DLOCAL._WHILE>.pm>
				
				CALL F_RUNE_COLOR <local.p>
				
				IF !(<ISEMPTY <lDB.ROW.<DLOCAL._WHILE>.region_text>>)
					DHTMLGUMP <EVAL (<DLOCAL.X>+<EVAL <LOCAL.MAIN_X>>)-280> <EVAL (<DLOCAL.Y>+<EVAL <LOCAL.MAIN_Y>>)-219> 200 17 0 0 <LOCAL.COLOR_HTML><CROPTEXT 13 <lDB.ROW.<DLOCAL._WHILE>.region_text>>
				ENDIF
				
				LOCAL.Y +=15
				
				IF (<LOCAL.Y> > 380)
					LOCAL.X +=160
					LOCAL.Y=260
				ENDIF	
				
			ENDIF
		ENDWHILE
	

	ENDIF
	
ENDIF
lDB.CLOSE


[DIALOG D_RUNEBOOK BUTTON]
ON=0
DIALOGCLOSE D_RUNEBOOK_LINKS

// RUNEBOOK RENAME
ON=1
DIALOGCLOSE D_RUNEBOOK_RENAME
DIALOG D_RUNEBOOK_RENAME
DCLICK
MORE2=


// rune ekle
ON=2
IF !(<TOPOBJ.UID> == <SRC.UID>)
	SRC.SYSMESSAGE @38,,1,1 Bu işlemi yapabilmeniz için runebookun çantanızda olması gerekmektedir.
	RETURN 1
ENDIF
MORE2=2
TARGET 
SRC.SYSMESSAGE @,,1,1 Mark olarak Eklemek istediğiniz Runeyi seçin.
SRC.SYSMESSAGE @030,,1,1 NOT : Runeyi sürükleyip Runebook üzerine bırakarak da kitaba rune ekleyebilirsiniz.
RETURN 1



// İLERİ
ON=3
TAG0.RUNEBOOK_LASTPAGE +=1
TAG0.RUNEBOOK_PAGE_INDEX +=18
MORE2=
DCLICK
SRC.SYSMESSAGE @,,1,1 Ileri > <DTAG0.RUNEBOOK_LASTPAGE>. sayfa




// GERİ
ON=4
IF (<TAG0.RUNEBOOK_LASTPAGE> <= 1)
	TAG0.RUNEBOOK_LASTPAGE=1
ELSE
	TAG0.RUNEBOOK_LASTPAGE -=1
ENDIF
IF (<TAG0.RUNEBOOK_PAGE_INDEX> > 0)
	TAG0.RUNEBOOK_PAGE_INDEX -=18
ELSE
	TAG0.RUNEBOOK_PAGE_INDEX 0
ENDIF
MORE2=
DCLICK
SRC.SYSMESSAGE @,,1,1 Geri < <DTAG0.RUNEBOOK_LASTPAGE>. sayfa



// Links
ON=5
DIALOGCLOSE D_RUNEBOOK
DIALOG D_RUNEBOOK,<TAG0.RUNEBOOK_LASTPAGE>
DIALOGCLOSE D_RUNEBOOK_LINKS
DIALOG D_RUNEBOOK_LINKS

// SEARCH PAGE NUM
ON=6
IF !(<ISNUM <ARGTXT[1]>>)
	SRC.SYSMESSAGE @38,,1,1 Bir sayı giriniz.
	TAG0.RUNEBOOK_LASTPAGE=1
	TAG0.RUNEBOOK_PAGE_INDEX=
	DIALOGCLOSE D_RUNEBOOK
	DIALOG D_RUNEBOOK
	RETURN 1
ENDIF
IF (<ARGTXT[1]> <= 0)
	SRC.SYSMESSAGE @38,,1,1 En az sıfır girebilirsiniz.
	TAG0.RUNEBOOK_LASTPAGE=1
	TAG0.RUNEBOOK_PAGE_INDEX=
	DIALOGCLOSE D_RUNEBOOK
	DIALOG D_RUNEBOOK
	RETURN 1
ENDIF
IF (<ARGTXT[1]> >= 50)
	SRC.SYSMESSAGE @38,,1,1 En fazla 50 girebilirsiniz.
	TAG0.RUNEBOOK_LASTPAGE=1
	TAG0.RUNEBOOK_PAGE_INDEX=
	DIALOGCLOSE D_RUNEBOOK
	DIALOG D_RUNEBOOK
	RETURN 1
ENDIF
IF (<STRSUB 0 1 <ARGTXT[1]>> == 0)
	SRC.SYSMESSAGE @38,,1,1 İlk rakama 0 yazamazsınız.
	TAG0.RUNEBOOK_LASTPAGE=1
	TAG0.RUNEBOOK_PAGE_INDEX=
	DIALOGCLOSE D_RUNEBOOK
	DIALOG D_RUNEBOOK
	RETURN 1
ENDIF
TAG0.RUNEBOOK_LASTPAGE=<ARGTXT[1]>
IF (<TAG0.RUNEBOOK_LASTPAGE> <= 1)
	TAG0.RUNEBOOK_PAGE_INDEX=0
ELSE
	TAG0.RUNEBOOK_PAGE_INDEX <EVAL (<ARGTXT[1]>*18)-18>
ENDIF
MORE2=
DCLICK
SRC.SYSMESSAGE @,,1,1 <DTAG0.RUNEBOOK_LASTPAGE>. sayfa




// GO PAGE: 1
ON=10
TAG0.RUNEBOOK_LASTPAGE=1
TAG0.RUNEBOOK_PAGE_INDEX <EVAL (<TAG0.RUNEBOOK_LASTPAGE>*18)-18>
DIALOGCLOSE D_RUNEBOOK
DIALOG D_RUNEBOOK

ON=11
TAG0.RUNEBOOK_LASTPAGE=2
TAG0.RUNEBOOK_PAGE_INDEX <EVAL (<TAG0.RUNEBOOK_LASTPAGE>*18)-18>
DIALOGCLOSE D_RUNEBOOK
DIALOG D_RUNEBOOK

ON=12
TAG0.RUNEBOOK_LASTPAGE=3
TAG0.RUNEBOOK_PAGE_INDEX <EVAL (<TAG0.RUNEBOOK_LASTPAGE>*18)-18>

DIALOGCLOSE D_RUNEBOOK
DIALOG D_RUNEBOOK

ON=13
TAG0.RUNEBOOK_LASTPAGE=4
TAG0.RUNEBOOK_PAGE_INDEX <EVAL (<TAG0.RUNEBOOK_LASTPAGE>*18)-18>
DIALOGCLOSE D_RUNEBOOK
DIALOG D_RUNEBOOK

ON=14
TAG0.RUNEBOOK_LASTPAGE=5
TAG0.RUNEBOOK_PAGE_INDEX <EVAL (<TAG0.RUNEBOOK_LASTPAGE>*18)-18>
DIALOGCLOSE D_RUNEBOOK
DIALOG D_RUNEBOOK

ON=15
TAG0.RUNEBOOK_LASTPAGE=6
TAG0.RUNEBOOK_PAGE_INDEX <EVAL (<TAG0.RUNEBOOK_LASTPAGE>*18)-18>
DIALOGCLOSE D_RUNEBOOK
DIALOG D_RUNEBOOK

ON=16
TAG0.RUNEBOOK_LASTPAGE=7
TAG0.RUNEBOOK_PAGE_INDEX <EVAL (<TAG0.RUNEBOOK_LASTPAGE>*18)-18>
DIALOGCLOSE D_RUNEBOOK
DIALOG D_RUNEBOOK

ON=17
TAG0.RUNEBOOK_LASTPAGE=8
TAG0.RUNEBOOK_PAGE_INDEX <EVAL (<TAG0.RUNEBOOK_LASTPAGE>*18)-18>
DIALOGCLOSE D_RUNEBOOK
DIALOG D_RUNEBOOK


// recall
ON=100 9999
LDB.CONNECT "db/runebooks.db"
IF (<LDB.CONNECTED>)
lDB.QUERY "SELECT rowid, id, rune_uid FROM '<SRC.CTAG0.RUNEBOOK_FROM>' WHERE id='<eval <DARGN>-100>'"
	IF (<dLDB.ROW.NUMROWS>)
		REF1=<LDB.ROW.0.rune_uid>
		
		IF (<TAG0.LINK>)
			EMPTY
			SERV.NEWITEM I_RUNE_MARKER
			NEW.CONT <UID>
			NEW.MOREP <REF1.MOREP>
			REF1=<NEW>
		ENDIF
		
		IF (<REF1>) && (<REF1.TYPE> == T_RUNE)
			SRC.EVENTS +E_RUNEBOOK_PLAYER
			REF1.MORE1 100
			SRC.ACTARG1 = 32      
			SRC.ACT = <REF1>
			SRC.ACTPRV = <UID>
			SRC.ACTP = <REF1.MOREX>,<REF1.MOREY>,<REF1.MOREZ>,<REF1.MOREM>
			SRC.ACTION = 25	
		ELSE
			SRC.SYSMESSAGE @38,,1,1 Bu slotta geçerli bir rune bulunamadı!
		ENDIF			
	ELSE
		SRC.SYSMESSAGE @38,,1,1 Bu slotta geçerli bir rune bulunamadı!
	ENDIF
LDB.CLOSE
ENDIF



// gate
ON=10000 19999
LDB.CONNECT "db/runebooks.db"
IF (<LDB.CONNECTED>)
lDB.QUERY "SELECT rowid, id, rune_uid FROM '<SRC.CTAG0.RUNEBOOK_FROM>' WHERE id='<EVAL <DARGN>-10000>'"
	IF (<dLDB.ROW.NUMROWS>)
		REF1=<LDB.ROW.0.rune_uid>
		
		IF (<TAG0.LINK>)
			EMPTY
			SERV.NEWITEM I_RUNE_MARKER
			NEW.CONT <UID>
			NEW.MOREP <REF1.MOREP>
			REF1=<NEW>
		ENDIF
		
		IF (<REF1>) && (<REF1.TYPE> == T_RUNE)
			SRC.EVENTS +E_RUNEBOOK_PLAYER
			REF1.MORE1 100
			SRC.ACTARG1 = 52      
			SRC.ACT = <REF1>
			SRC.ACTPRV = <UID>
			SRC.ACTP = <REF1.MOREX>,<REF1.MOREY>,<REF1.MOREZ>,<REF1.MOREM>
			SRC.ACTION = 25		
		ELSE
			SRC.SYSMESSAGE @38,,1,1 Bu slotta geçerli bir rune bulunamadı!
		ENDIF
	ELSE
		SRC.SYSMESSAGE @38,,1,1 Bu slotta geçerli bir rune bulunamadı!
	ENDIF
LDB.CLOSE
ENDIF





// rune cikart
ON=20000 29999
LDB.CONNECT "db/runebooks.db"
IF (<LDB.CONNECTED>)


	lDB.QUERY "SELECT rowid, id, rune_uid,region_name,region_text FROM '<SRC.CTAG0.RUNEBOOK_FROM>' WHERE id='<EVAL <DARGN>-20000>'"
	
	IF (<lDB.ROW.NUMROWS>)	
		REF1=<lDB.ROW.0.rune_uid>

		IF (<REF1>) && (<REF1.TYPE> == T_RUNE)
			SRC.SYSMESSAGE @044,,1,1 [Runebook] Rune/Slot Çıkart - Slot:<ldb.row.0.id> (<ldb.row.0.region_name> - <ldb.row.0.region_text>)

			LDB.EXECUTE "UPDATE '<SRC.CTAG0.RUNEBOOK_FROM>' SET rune_uid=NULL, rune_name=NULL, rune_morep=NULL, rune_px=NULL, rune_py=NULL, rune_pz=NULL, rune_pm=NULL, region_name=NULL, region_text=NULL, region_flags=NULL, page=NULL, ordering=NULL WHERE id='<EVAL <DARGN>-20000>'"
			
			REF1.CONT=<SRC.FINDLAYER.21>
			REF1.UPDATEX
			UPDATEX
			MORE2=
			SRC.DCLICK <UID>
			
		ELSE
			SRC.SYSMESSAGE @38,,1,1 Bu slotta geçerli bir rune bulunamadı!
		ENDIF
		
	ELSE
		SRC.SYSMESSAGE @38,,1,1 Bu slotta geçerli bir rune bulunamadı!
	ENDIF
	
LDB.CLOSE
ENDIF



// rune name
ON=30000 39999
src.sysmessage <EVAL <dargn>-30000> 
LDB.CONNECT "db/runebooks.db"
IF (<LDB.CONNECTED>)
	lDB.Query "SELECT id, rune_uid, region_name, region_text FROM '<SRC.CTAG0.RUNEBOOK_FROM>' WHERE id='<EVAL <dargn>-30000>'"

	IF (<lDB.ROW.NUMROWS>)	
		
		REF1=<LDB.ROW.0.rune_uid>

		IF (<REF1>) && (<REF1.TYPE> == T_RUNE)
			SRC.SYSMESSAGE @044,,1,1 [Runebook] Rune/Slot Rename - Slot:<ldb.row.0.id> (<ldb.row.0.region_name> - <ldb.row.0.region_text>)
			REF1.DIALOGCLOSE D_RUNEBOOK_LOCALNAME
			REF1.DIALOG D_RUNEBOOK_LOCALNAME	
		ELSE
			SRC.SYSMESSAGE @38,,1,1 Bu slotta geçerli bir rune bulunamadı!
		ENDIF
	ELSE
		SRC.SYSMESSAGE @38,,1,1 Bu slot boş.
	ENDIF
ENDIF
DB.CLOSE







[EVENTS E_RUNEBOOK_PLAYER]
ON=@SPELLSELECT
IF (<DARGN1> == 32) || (<DARGN1> == 52)
	SRC.EVENTS -E_RUNEBOOK_PLAYER
	RETURN 6 // Reg gereksinimini kaldırır
ENDIF

ON=@SPELLFAIL
SRC.EVENTS -E_RUNEBOOK_PLAYER






[DIALOG D_RUNEBOOK_RENAME]               
<DEF.DSP>
RESIZE 0,0,260,120,Runebook İsim Değiştirme

IF (<ISBLANKSTRING <TAG0.NAME>>)
	DENTRYLIMIT 30,45,200,25,0480,0,25,<name>
ELSE
	DENTRYLIMIT 30,45,200,25,0480,0,25,<TAG0.name>
ENDIF

DBUTTON 30,80,4006,4006,1,0,1,<DEF.SMSG_SUCCESS_COLOR>,Tamam
DBUTTON 135,80,4006,4007,1,0,2,<DEF.SMSG_CUSTOM_COLOR>,Varsayılan

[DIALOG D_RUNEBOOK_RENAME BUTTON]
ON=0
// SRC.SYSMESSAGE @030,,1,1 Rune ismini değiştirmekten vazgeçtin.

ON=1
IF (<ISEMPTY <ARGTXT[0]>>)
	SRC.SYSMESSAGE @38,,1,1 <def.systxt_only_empty>
	RETURN 1
ELSEIF (<ISBADCHAR <ARGTXT[0]>>)
	SRC.SYSMESSAGE @38,,1,1 <def.systxt_using_bad>
	RETURN 1
ELSEIF (<ISBLANKSTRING <ARGTXT[0]>>)
	SRC.SYSMESSAGE @38,,1,1 <def.systxt_only_empty>
	RETURN 1
ELSEIF !(<ISALPHANUMERIC <ARGTXT[0]>>)
	SRC.SYSMESSAGE @38,,1,1 <DEF.systxt_only_str_or_int>
	RETURN 1
ELSEIF (<EVAL STRLEN(<ARGTXT[0]>)> < 3) || (<EVAL STRLEN(<ARGTXT[0]>)> > 25)
	SRC.SYSMESSAGE @38,,1,1 Runebook adı en az 3 en fazla 25 karakterden oluşabilir.
	RETURN 1
ENDIF
TAG0.NAME=<ARGTXT[0]>
MORE2=
SRC.DCLICK <UID>
RETURN 1

ON=2
TAG0.NAME=
SRC.SYSMESSAGE @,,1,1 Runebook adı varsayılan olarak değişti.
MORE2=
SRC.DCLICK <UID>
RETURN 1












[DIALOG D_RUNEBOOK_LOCALNAME]               
<DEF.DSP>
RESIZE 0,0,240,120,Rune İsim Değiştirme

LDB.CONNECT "db/runebooks.db"
	IF (<LDB.CONNECTED>)
	lDB.QUERY "SELECT rowid, id, region_text FROM '<SRC.CTAG0.RUNEBOOK_FROM>' WHERE rune_uid='<UID>'"
		IF (<dLDB.ROW.NUMROWS>)
			DENTRYLIMIT 20,45,200,25,0480,0,25,<ldb.row.0.region_text>
		ENDIF
	LDB.CLOSE
ENDIF

DBUTTON 20,80,4006,4006,1,0,1,<DEF.SMSG_SUCCESS_COLOR>,Tamam
DBUTTON 125,80,4006,4007,1,0,2,<DEF.SMSG_CUSTOM_COLOR>,Varsayılan


[DIALOG D_RUNEBOOK_LOCALNAME BUTTON]
ON=0
SRC.SYSMESSAGE @030,,1,1 Rune ismini değiştirmekten vazgeçtin.

ON=1
IF (<ISBADCHAR <ARGTXT[0]>>)
	SRC.SYSMESSAGE @38,,1,1 <def.systxt_using_bad>
	RETURN 1
ELSEIF (<ISBLANKSTRING <ARGTXT[0]>>)
	SRC.SYSMESSAGE @38,,1,1 <def.systxt_only_empty>
	RETURN 1
ELSEIF !(<ISALPHANUMERIC <ARGTXT[0]>>)
	SRC.SYSMESSAGE @38,,1,1 <DEF.systxt_only_str_or_int>
	RETURN 1
ELSEIF (<EVAL STRLEN(<ARGTXT[0]>)> < 3) || (<EVAL STRLEN(<ARGTXT[0]>)> > 25)
	SRC.SYSMESSAGE @38,,1,1 Rune adı en az 3 en fazla 25 karakterden oluşabilir.
	RETURN 1
ENDIF

LDB.CONNECT "db/runebooks.db"
IF (<LDB.CONNECTED>)
	lDB.QUERY "SELECT rowid, id FROM '<SRC.CTAG0.RUNEBOOK_FROM>' WHERE rune_uid='<UID>'"
	IF (<dLDB.ROW.NUMROWS>)
		LDB.EXECUTE "UPDATE '<SRC.CTAG0.RUNEBOOK_FROM>' SET region_text='<StrReplace <ARGTXT[0]>>' WHERE rune_uid='<UID>'"
		SRC.SYSMESSAGE @,,1,1 Rune adı <STRING_REPLACER <ARGTXT[0]>> olarak değişti.
		// MORE2=
		// SRC.DCLICK <UID>
	ENDIF
LDB.CLOSE
ENDIF
RETURN 1


ON=2
LDB.CONNECT "db/runebooks.db"
IF (<LDB.CONNECTED>)
lDB.QUERY "SELECT rowid, id FROM '<SRC.CTAG0.RUNEBOOK_FROM>' WHERE rune_uid='<UID>'"
	IF (<dLDB.ROW.NUMROWS>)
		lDB.EXECUTE "UPDATE '<SRC.CTAG0.RUNEBOOK_FROM>' SET region_text='<StrReplace <SERV.MAP(<Ldb.row.0.region_p>).REGION.NAME>>' WHERE rune_uid='<UID>'"
		SRC.SYSMESSAGE @,,1,1 Rune adı varsayılan olarak değişti.
		// MORE2=
		// SRC.DCLICK <UID>
	ENDIF
LDB.CLOSE
ENDIF
RETURN 1



[ITEMDEF I_RUNEBOOK_GATE]
DEFNAME=I_RUNEBOOK_GATE
ID=I_MOONGATE_BLACK
TYPE=T_RUNEBOOK_GATE
NAME=Runebook Gate

ON=@CREATE
COLOR=0845
ATTR=ATTR_MOVE_NEVER|ATTR_DECAY
MORE1=45
TIMER=1
TYPE=T_RUNEBOOK_GATE



[TYPEDEF T_RUNEBOOK_GATE]
ON=@DCLICK
TRIGGER @STEP
RETURN 1

ON=@STEP
IF (<SRC.ISNOMOVERFLAGS>)
	SRC.SYSMESSAGE @38,,1,1 Bu durumda bunu kullanamazsınız.
	RETURN 1
ENDIF

IF (<SRC.DISTANCE <UID>> > 3)
	SRC.SYSMESSAGE @38,,1,1 <NAME>: <def.reach_fail>
	
ELIF !(<SRC.CANSEELOS <UID>>)
	SRC.SYSMESSAGE @38,,1,1 <NAME>: <def.reach_fail>
ELSE
	SDIALOG D_RUNE_GATE
ENDIF

ON=@CLICK
MESSAGE [<LINK.P>]
MESSAGE <LINK.REGION.NAME>
RETURN 1

ON=@TIMER
IF (<MORE1>)
	MORE1 --
	TIMER=1
ELSE
	SOUND 528
	REMOVE
ENDIF
RETURN 1




[DIALOG D_RUNE_GATE]
<DEF.DSP>
RESIZEPIC 0 0 5120 260 150
RESIZEPIC 10 10 3000 240 130

DHTMLGUMP 20 20 220 25 1 0 <DEF.CENTER>GATE TRAVEL

TILEPIC 20 60 <SERV.ITEMDEF.I_RUNE_GATE_TRAVEL.ID>

IF (<LINK>) && (<LINK.ISITEM>)
	CALL Map_Colors <LINK.P.X>,<LINK.P.Y>,<LINK.P.Z>,<LINK.P.M>
	DHTMLGUMP 70 50 170 25 1 0 <LOCAL.REGIONCOLOR_HTML><LINK.REGION.NAME>
	DHTMLGUMP 70 80 170 25 1 0 <LINK.P>
	// IF (<LINK.ISNEARTYPE T_SPELL 1>)
		// DTEXT 30 105 <DEF.SMSG_ERROR_COLOR> * Gidiş yolu tehlikeli *
	// ENDIF
ENDIF

BUTTON 70 110 4006 4007 1 0 1
DTEXT 100 110 <DEF.SMSG_SUCCESS_COLOR> Evet

BUTTON 170 110 0FB2 0FB1 1 0 2 
TEXT 200 110 <DEF.SMSG_ERROR_COLOR> Hayır



[DIALOG D_RUNE_GATE BUTTON]
ON=2
SRC.SYSMESSAGE @38,,1,1 Gateden geçmekten vazgeçtin.

ON=0
REF1=<UID>
IF (<REF1>)
	SRC.SYSMESSAGE @38,,1,1 Gateden geçmekten vazgeçtin.
ELSE
	SRC.SYSMESSAGE @38,,1,1 Gate kapandı!
ENDIF

ON=1
SRC.DIALOGCLOSE D_RUNE_GATE
REF1=<UID>

IF !(<REF1>)
	SRC.SYSMESSAGE @38,,1,1 Gate kapanmış.

ELSEIF (<SRC.DISTANCE <UID>> > 3)
	SRC.SYSMESSAGE @38,,1,1 <NAME>: <def.reach_fail>

ELSEIF !(<SRC.CANSEELOS <UID>>)
	SRC.SYSMESSAGE @38,,1,1 Gateye ulaşamıyorsunuz.
	
ELSEIF !(<MOREX>)
	SRC.SYSMESSAGE @38,,1,1 Bu gate bir yere gitmiyor.
		
ELSEIF !(<LINK>)
	SRC.SYSMESSAGE @38,,1,1 Bu gatenin karşılığı yok.
	
ELSE
	SRC.GO <MOREP>
	// SRC.MOVETO <MOREP>
ENDIF
SRC.DIALOGCLOSE D_RUNE_GATE


[ITEMDEF I_RUNEBOOK_REGION_CHECKER]
DEFNAME=I_RUNEBOOK_REGION_CHECKER
ID=I_GOLD
NAME=Region Controller
TYPE=T_SCRIPT

ON=@CREATE
ATTR ATTR_INVIS|ATTR_DECAY|ATTR_MOVE_NEVER
COLOR=07E5
TIMER=10

ON=@TIMER
REMOVE
RETURN 1


[EOF]