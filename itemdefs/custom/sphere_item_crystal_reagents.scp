

// magery reagents

[ITEMDEF i_magic_reg30]
ID=i_crystal_green_big 
Value=1950
WEIGHT=0
NAME=reg crystal [30]
TYPE=T_REAGENT_CRYSTAL

ON=@Create
MORE1=30
TAG0.REAGENT_ITEMS=<DEF.ALL_MAGERY_REAGENTS>
COLOR=0
ATTR=ATTR_NEWBIE


[ITEMDEF i_magic_reg60]
ID=i_crystal_green_big 
Value=3600
WEIGHT=0
NAME=reg crystal [60]
TYPE=T_REAGENT_CRYSTAL

ON=@Create
MORE1=60
TAG0.REAGENT_ITEMS=<DEF.ALL_MAGERY_REAGENTS>
COLOR=0
ATTR=ATTR_NEWBIE


[ITEMDEF i_magic_reg90]
ID=i_crystal_green_big 
Value=6000
WEIGHT=0
NAME=reg crystal [90]
TYPE=T_REAGENT_CRYSTAL

ON=@Create
MORE1=90
TAG0.REAGENT_ITEMS=<DEF.ALL_MAGERY_REAGENTS>
COLOR=0
ATTR=ATTR_NEWBIE

[ITEMDEF i_magic_reg120]
ID=i_crystal_green_big 
Value=10900
WEIGHT=0
NAME=reg crystal [120]
TYPE=T_REAGENT_CRYSTAL
  

ON=@Create
MORE1=120 
TAG0.REAGENT_ITEMS=<DEF.ALL_MAGERY_REAGENTS>
COLOR=0
ATTR=ATTR_NEWBIE


[TYPEDEF T_REAGENT_CRYSTAL]
ON=@Click
ATTR=ATTR_NEWBIE
LOCAL.AMOUNT = <MORE1>

IF (<LOCAL.AMOUNT> == 30)
    MESSAGE a lesser reagent crystal (<DLOCAL.AMOUNT>)

ELSEIF (<LOCAL.AMOUNT> == 60)
    MESSAGE a reagent crystal (<DLOCAL.AMOUNT>)

ELSEIF (<LOCAL.AMOUNT> == 90)
    MESSAGE a greater reagent crystal (<DLOCAL.AMOUNT>)

ELSEIF (<LOCAL.AMOUNT> == 120)
    MESSAGE a greater reagent crystal (<DLOCAL.AMOUNT>)
ELSE
    MESSAGE a custom reagent crystal (<DLOCAL.AMOUNT>)
ENDIF
IF (<SRC.ISINPACK <UID>>)
	DIALOGCLOSE D_REAGENT_CRYSTAL
	DIALOG D_REAGENT_CRYSTAL
ELSE
	DIALOGCLOSE D_REAGENT_CRYSTAL
ENDIF
RETURN 1


ON=@DROPON_GROUND
DIALOGCLOSE D_REAGENT_CRYSTAL

ON=@DClick
ATTR=ATTR_NEWBIE
LOCAL.AMOUNT = <MORE1>

IF !(<SRC.ISINPACK <UID>>)
	SRC.SYSMESSAGE @38,,1,1 <NAME>: <DEF.UNPACKMSG>
	RETURN 1
ELSEIF (<SRC.WEIGHT> >= <SRC.MAXWEIGHT>)
	SRC.SYSMESSAGE @38,,1,1 Ağırlığınız maksimum seviyeye ulaştığı için bunu kullanamazsınız.
	RETURN 1
ENDIF
IF (<ISEMPTY <TAG0.REAGENT_ITEMS>>)
	TAG0.REAGENT_ITEMS=<DEF.ALL_MAGERY_REAGENTS>
ENDIF

IF (<DLOCAL.AMOUNT>)

	IF !(<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE>)
		SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE=1
		SRC.SYSMESSAGE @030,,1,1 Reagentların varsayılan çıkartma yeri çantanız olarak ayarlandı. Değiştirmek için tek tıklayınız.
		LOCAL.CONTAINER=<SRC.FINDLAYER.21>
	
	ELSEIF (<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE> == 1)
		LOCAL.CONTAINER=<SRC.FINDLAYER.21>
		
	ELSEIF (<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE> == 2)
		SERV.NEWITEM I_BAG
			IF (<SRC.FINDLAYER.21>)
				NEW.CONT=<SRC.FINDLAYER.21>
			ENDIF
		NEW.CONTP <CONTP>
		LOCAL.CONTAINER=<NEW.UID>
		
	ELSEIF (<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE> == 3)
		LOCAL.CONTAINER=<SRC.TAG0.REAGENT_CRYSTAL.CONTAINER>
		
	ELSEIF (<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE> == 4)
		LOCAL.CONTAINER=<SRC.TAG0.REAGENT_CRYSTAL.CONTAINER>
		
	ELSE
		SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE=1
		SRC.SYSMESSAGE @030,,1,1 Reagentların varsayılan çıkartma yeri çantanız olarak ayarlandı. Değiştirmek için tek tıklayınız.
		LOCAL.CONTAINER=<SRC.FINDLAYER.21>
		
	ENDIF
	
	REF99=<LOCAL.CONTAINER>
	
	IF !(<SRC.ISINPACK <REF99>>)
		SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE=1
		SRC.SYSMESSAGE @030,,1,1 Reagentların varsayılan çıkartma yeri çantanız olarak ayarlandı. Değiştirmek için tek tıklayınız.
		LOCAL.CONTAINER=<SRC.FINDLAYER.21>
	ENDIF
	
	FOR 1 <ARRAYCOUNT <TAG0.REAGENT_ITEMS>>
		SERV.NEWITEM <ARRAY <TAG0.REAGENT_ITEMS>,<DLOCAL._FOR>>,<DLOCAL.AMOUNT>,<LOCAL.CONTAINER>,1
		NEW.CONTP <CONTP>
	ENDFOR

	IF (<DLOCAL.AMOUNT> == 30)
		SRC.EMOTE use lesser reagent crystal
	ELSEIF (<DLOCAL.AMOUNT> == 60)
		src.EMOTE use reagent crystal
	ELSEIF (<DLOCAL.AMOUNT> == 90)
		SRC.EMOTE use greater reagent crystal
	ELSEIF (<DLOCAL.AMOUNT> == 120)
		SRC.EMOTE use greater reagent crystal
	ELSE
		SRC.EMOTE use custom reagent crystal
	ENDIF

	REMOVE

	IF (<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE> == 1)
		SRC.SYSMESSAGE @044,,1,1 Reagentlar çantanıza çıkartıldı!
	ELSEIF (<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE> == 2)
		SRC.SYSMESSAGE @044,,1,1 Reagentlar yeni bag içine çıkartıldı.
	ELSEIF (<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE> == 3)
		SRC.SYSMESSAGE @044,,1,1 Reagentlar seçtiğiniz çanta içine çıkartıldı.
	ELSEIF (<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE> == 4)
		SRC.SYSMESSAGE @044,,1,1 Reagentlar seçtiğiniz çanta içine çıkartıldı.
	ENDIF
	
ELSE
	SRC.SYSMESSAGE @38,,1,1 Hatalı <NAME>!
ENDIF

RETURN 1


[DIALOG D_REAGENT_CRYSTAL]
<DEF.DlgStartPos>

RESIZEPIC 0 0 9200 280 190
RESIZEPIC 10 10 3000 260 170
DHTMLGUMP 10 10 260 30 1 0 <DEF.CENTER><StrTitle <NAME>><DEF.CENTERE>

DORIGIN 20 50

IF (<SRC.FINDLAYER.21>)
    TILEPIC +0 +0 <SRC.FINDLAYER.21.ID>
    IF (<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE> == 1)
        BUTTON +50 +0 4005 4007 1 0 1
    ELSE
        BUTTON +50 +0 4006 4007 1 0 1
    ENDIF
    DTEXT +80 +0 0 Çantama çıkart
ENDIF

TILEPIC +0 80 <SERV.ITEMDEF.I_BAG.ID>

IF (<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE> == 2)
    BUTTON +50 +30 4005 4007 1 0 2
ELSE
    BUTTON +50 +30 4006 4007 1 0 2
ENDIF
DTEXT +80 +30 0 Bag içinde çıkart


IF (<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE> == 3)
    BUTTON +50 +60 4005 4007 1 0 3
ELSE
    BUTTON +50 +60 4006 4007 1 0 3
ENDIF
DTEXT +80 +60 0 Seçtiğim çantaya çıkart

IF (<CONT>)
	TILEPIC +0 +90 <CONT.ID>
	IF (<SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE> == 4)
        BUTTON +50 +90 4005 4006 1 0 4
	ELSE
        BUTTON +50 +90 4006 4007 1 0 4
	ENDIF
    DTEXT +80 +90 0 Bulunduğu yere çıkart
ENDIF

[DIALOG D_REAGENT_CRYSTAL BUTTON]
ON=1
IF (<SRC.FINDLAYER.21>)
    SRC.TAG0.REAGENT_CRYSTAL.CONTAINER=<SRC.FINDLAYER.21>
    SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE=1
    SRC.SYSMESSAGE @,,1,1 Bundan sonra tüm reagent crystal kullanımlarınızda reagentlar çantanıza düşecektir.
ENDIF

ON=2
SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE=2
SRC.TAG0.REAGENT_CRYSTAL.CONTAINER=
SRC.SYSMESSAGE @,,1,1 Bundan sonra tüm reagent crystal kullanımlarınızda yeni bir çantaya düşecektir.

ON=3
SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE=3
SRC.TARGETF REAGENT_CONTAINER_SELECT_ITEM
SRC.SYSMESSAGE @,,1,1 Bir çanta/kutu/kasa seçiniz.

ON=4
IF (<CONT>)
    SRC.TAG0.REAGENT_CRYSTAL.CONTTYPE=4
    SRC.TAG0.REAGENT_CRYSTAL.CONTAINER=<CONT>
    SRC.SYSMESSAGE @,,1,1 Bundan sonra tüm reagent crystal kullanımlarınızda bulunduğu yere düşecektir.
ENDIF



[FUNCTION REAGENT_CONTAINER_SELECT_ITEM]
IF !(<ARGO>)
    SRC.SYSMESSAGE @38,,1,1 Bu bir eşya değil.
    RETURN 1
ELSEIF !(<ARGO.ISITEM>)
    SRC.SYSMESSAGE @38,,1,1 Bu bir eşya değil.
    RETURN 1
ELSEIF !(<ARGO.TYPE> == T_CONTAINER)
    SRC.SYSMESSAGE @38,,1,1 Bu bir çanta/kutu/kasa değil.
    RETURN 1
ELSEIF !(<SRC.ISINPACK <ARGO>>)
    SRC.SYSMESSAGE @38,,1,1 Bu bir çanta/kutu/kasa yanınızda değil.
    RETURN 1
ENDIF
SRC.SYSMESSAGE @,,1,1 Bundan sonra tüm reagent crystal kullanımlarınızda seçtiğiniz çantaya düşecektir.
SRC.TAG0.REAGENT_CRYSTAL.CONTAINER=<ARGO>

[EOF]