

[typedef t_check_general]
On=@Click
COLOR=<SERV.ITEMDEF.<BASEID>.RESDISPDNHUE>
REF1=<OWNEDBY>
IF (<REF1>)
	MESSAGE @0492,,1 [Yazan: <REF1.NAME>]
ELSE
	MESSAGE @,,1,1 Karşılıksız çek
ENDIF
MESSAGE @,,1 <NAME> <QVAL (<MORE2> <= 0)?:[<NumberFormat <dMORE2>> <SERV.ITEMDEF.<MORE1>.NAME>]>
RETURN 1


ON=@DCLICK
IF !(<TOPOBJ.UID> == <SRC>)
	SRC.SYSMESSAGE @38,,1,1 <NAME>: <defmsg.item_unpack>
	RETURN 1
ELSEIF (<SRC.ISPOISONED>)
	SRC.SYSMESSAGE @38,,1,1 Zehirliyken bunu kullanamazsınız.
	RETURN 1
ELSEIF (<SRC.ISNOFULLHP>)
	SRC.SYSMESSAGE @38,,1,1 Yaralıyken bunu kullanamazsınız.
ELSEIF !(<MORE2>)
	SRC.SYSMESSAGE @38,,1,1 Bu çek değersiz.
	REMOVE
	RETURN 1
ENDIF
IF !(<SERV.FeatureTOL>&02)
	IF (<MORE2> > 10000000)
		SRC.SYSMESSAGE @38,,1,1 Tek seferde en fazla 10.000.000 check bozabilirsiniz.
		RETURN 1
	ENDIF	
ENDIF
COLOR=<SERV.ITEMDEF.<BASEID>.RESDISPDNHUE>
ATTR=ATTR_NEWBIE

REF1=<OWNEDBY> // YAZAN
IF (<MORE2> > 0)

	IF (<MORE1> == i_gold)
		LOCAL.NAME = GOLD
	ELSEIF (<MORE1> == i_coin_silver)
		LOCAL.NAME SILVER
	ELSEIF (<MORE1> == i_coin_copper)
		LOCAL.NAME COPPER
	ENDIF
	
	IF (<SERV.FeatureTOL>&02)
		SRC.VIRTUAL<LOCAL.NAME> +=<DMORE2>
		SRC.SYSMESSAGE @,,1,1 Çek miktarı (<NumberFormat <DMORE2>> <SERV.ITEMDEF.<MORE1>.NAME>) sanal hesabınıza aktarılmıştır. Bankanızı açarak sanal paralarınızı görebilirsiniz.
	ELSE
		SRC.<LOCAL.NAME> +=<DMORE2>
	ENDIF

	SRC.CUSTOMLOG CEK BOZDU: <UID> [<NumberFormat <dMORE2>> <SERV.ITEMDEF.<MORE1>.NAME>] <QVAL (<REF1>)?YAZAN:<REF1.NAME>:> [<REF1>] SAHIBI:<TOPOBJ.NAME> (<TOPOBJ>)
ELSE
	SRC.SYSMESSAGE @38,,1,1 Çek miktarı olmadığı için silindi.
	SRC.CUSTOMLOG HATALI CHECK: <UID> (<MORE2>) YAZAN:<UID.<OWNEDBY>.NAME> (<MORE2>) SAHIBI:<TOPOBJ.NAME> (<TOPOBJ>)
ENDIF
REMOVE
RETURN 1





[ITEMDEF I_CHECK_COPPER]
DEFNAME=I_CHECK_COPPER
ID=I_DEED
TYPE=t_check_general
WEIGHT=0.1
NAME=Check
RESDISPDNHUE=0796

On=@Create
COLOR=<SERV.ITEMDEF.<BASEID>.RESDISPDNHUE>
ATTR=ATTR_NEWBIE
IF (<SRC>)
	OWNEDBY=<SRC.UID>
ENDIF
MORE1=i_coin_copper
MORE2=0

[ITEMDEF I_CHECK_SILVER]
DEFNAME=I_CHECK_SILVER
ID=I_DEED
TYPE=t_check_general
WEIGHT=0.1
NAME=Check
RESDISPDNHUE=0482

On=@Create
COLOR=<SERV.ITEMDEF.<BASEID>.RESDISPDNHUE>
ATTR=ATTR_NEWBIE
IF (<SRC>)
	OWNEDBY=<SRC.UID>
ENDIF
MORE1=i_coin_silver
MORE2=0

[ITEMDEF I_CHECK_GOLD]
DEFNAME=I_CHECK_GOLD
ID=I_DEED
TYPE=t_check_general
WEIGHT=0.1
NAME=Check
RESDISPDNHUE=0

On=@Create
COLOR=<SERV.ITEMDEF.<BASEID>.RESDISPDNHUE>
ATTR=ATTR_NEWBIE
IF (<SRC>)
	OWNEDBY=<SRC.UID>
ENDIF
MORE1=i_gold
MORE2=0

[EOF]