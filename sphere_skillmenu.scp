


[PLEVEL 1]
MAKELAST

[FUNCTION MAKELAST]
LOCAL.MAKEITEM = <TAG0.CRAFT_LASTMAKE_BASEID>
LOCAL.MAKEAMOUNT = <QVAL (<ISEMPTY <ARGS>>)?1:<ARGS>>

IF !(<ACTION> == -1)
	SYSMESSAGE @38,,1,1  Şu an zaten bir beceri kullanıyorsunuz.
	CTAG.Craft_Menu.Notice = <DEF.BFONT_RED>Şu an zaten bir beceri kullanıyorsunuz.
	RETURN 1
	
ELSEIF !(<TAG0.CRAFT_LASTMAKE_BASEID>)
	SYSMESSAGE @38,,1,1  En son ne yaptığınızı hatırlamıyorsunuz.
	CTAG.Craft_Menu.Notice = <DEF.BFONT_RED>En son ne yaptığınızı hatırlamıyorsunuz.
	RETURN 1
	
ELSEIF (<LOCAL.MAKEITEM> == 0) || !(<LOCAL.MAKEITEM>)
	SYSMESSAGE @38,,1,1  En son ne yaptığınızı hatırlamıyorsunuz.
	CTAG.Craft_Menu.Notice = <DEF.BFONT_RED>En son ne yaptığınızı hatırlamıyorsunuz.
	RETURN 1
	
ELSEIF (<LOCAL.MAKEAMOUNT> > 50000)
	SYSMESSAGE @38,,1,1  En fazla 50.000 kere tekrar yapabilirsiniz.
	CTAG.Craft_Menu.Notice = <DEF.BFONT_RED>En fazla 50.000 kere tekrar yapabilirsiniz.
	RETURN 1

ELSEIF !(<CANMAKESKILL <LOCAL.MAKEITEM>>)
	SYSMESSAGE @38,,1,1  [<serv.itemdef.<LOCAL.MAKEITEM>.name>] Yeterli beceriye sahip değilsin. (<SERV.ITEMDEF.<LOCAL.MAKEITEM>.SKILLMAKE>)
	CTAG.Craft_Menu.Notice = <DEF.BFONT_RED>[<serv.itemdef.<LOCAL.MAKEITEM>.name>] Yeterli beceriye sahip değilsin. (<SERV.ITEMDEF.<LOCAL.MAKEITEM>.SKILLMAKE>)
	RETURN 1

ELSEIF (<ISEVENT.E_MAKELAST>)
	SYSMESSAGE @38,,1,1  Şu an zaten yaptıklarınız tekrarlanıyor.
	CTAG.Craft_Menu.Notice = <DEF.BFONT_RED>Şu an zaten yaptıklarınız tekrarlanıyor.
	RETURN 1

ELSEIF !(<CANMAKE <LOCAL.MAKEITEM>>)
	SYSMESSAGE @38,,1,1  Yeterli malzemeye sahip değilsin. (<SERV.ITEMDEF.<LOCAL.MAKEITEM>.RESMAKE>)
		CTAG.Craft_Menu.Notice = <DEF.BFONT_RED>Yeterli malzemeye sahip değilsin. (<SERV.ITEMDEF.<LOCAL.MAKEITEM>.RESMAKE>)
	RETURN 1
ENDIF

// IF (<WEIGHT> >= <MAXWEIGHT>)
	// SYSMESSAGE @38,,1,1  Olabilecek en yüksek ağırlığa ulaştığınız için eşyayı üretemezsiniz. (<FVAL <WEIGHT>>/<FVAL <MAXWEIGHT>>)
	// CTAG.Craft_Menu.Notice = <DEF.BFONT_RED>Olabilecek en yüksek ağırlığa ulaştığınız için eşyayı üretemezsiniz. (<FVAL <WEIGHT>>/<FVAL <MAXWEIGHT>>)
	// RETURN 1
// ENDIF

IF (<ISONLINE>)

	IF (<LOCAL.MAKEAMOUNT> > 1)
		EVENTS +E_MAKELAST
	ENDIF
	
	IF (<ISEMPTY <ARGS>>)
		SYSMESSAGE @084A,,1,1  .makelast 5000 olarak da komutu kullanabilirsiniz. 5000 tekrar otomatik olarak yapılacaktır. 5000 yerine 2 ile 50.000 arasında herhangi bir rakam girebilirsiniz.
	ENDIF
	
	MAKEITEM <LOCAL.MAKEITEM>
	CTAG0.CRAFT_NOTICE <DEF.BFONT_GREEN><SERV.ITEMDEF.<LOCAL.MAKEITEM>.NAME> isimli eşya tekrar yapılıyor.
	CTAG0.MAKE_TEKRAR = <LOCAL.MAKEAMOUNT>
	CTAG0.MAKE_TEKRAR_BASE <dCTAG0.MAKE_TEKRAR>
	CTAG.Craft_Menu.Notice = <DEF.BFONT_GREEN>Son yaptığınız eşya yapılıyor. (<SERV.ITEMDEF.<LOCAL.MAKEITEM>.NAME>)
			
	IF !(<ISDIALOGOPEN D_CRAFT_MENU>)
		SYSMESSAGE @044,,1,1  Son yaptığınız eşya yapılıyor. (<SERV.ITEMDEF.<LOCAL.MAKEITEM>.NAME>)
		
		IF (<LOCAL.MAKEAMOUNT> > 1)
			SYSMESSAGE @033,,1,1  Tekrar sayisi: <dCTAG0.MAKE_TEKRAR>
			SYSMESSAGE @033,,1,1  İslemi iptal etmek icin savas moduna gecmeniz yeterlidir.
		ENDIF
	ELSE
		DIALOGCLOSE D_CRAFT_MENU
		DIALOG D_CRAFT_MENU
	ENDIF
	
ENDIF




[EVENTS e_makelast]
ON=@USERWARMODE
SRC.CTAG0.MAKE_TEKRAR =
SRC.SYSMESSAGE @38,,1,1  Tekrar işleminiz sona erdi.
SRC.CTAG.Craft_Menu.Notice = <DEF.BFONT_LRED>Tekrar işleminiz sona erdi.
SRC.EVENTS -E_MAKELAST
IF (<ISDIALOGOPEN D_CRAFT_MENU>)
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
ENDIF
	

ON=@DEATH
SRC.CTAG0.MAKE_TEKRAR =
SRC.SYSMESSAGE @38,,1,1  Tekrar işleminiz sona erdi.
SRC.CTAG.Craft_Menu.Notice = <DEF.BFONT_LRED>Tekrar işleminiz sona erdi.
SRC.EVENTS -E_MAKELAST
IF (<ISDIALOGOPEN D_CRAFT_MENU>)
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
ENDIF


ON=@LOGOUT
SKILL FAIL
SRC.CTAG0.MAKE_TEKRAR =
SRC.SYSMESSAGE @38,,1,1  Tekrar işleminiz sona erdi.
SRC.CTAG.Craft_Menu.Notice = <DEF.BFONT_LRED>Tekrar işleminiz sona erdi.
SRC.EVENTS -E_MAKELAST
IF (<ISDIALOGOPEN D_CRAFT_MENU>)
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
ENDIF


ON=@ATTACK
CTAG0.MAKE_TEKRAR =
SYSMESSAGE @38,,1,1  Tekrar işleminiz sona erdi.
CTAG.Craft_Menu.Notice = <DEF.BFONT_LRED>Tekrar işleminiz sona erdi.
EVENTS -E_MAKELAST
IF (<ISDIALOGOPEN D_CRAFT_MENU>)
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
ENDIF


ON=@SPELLCAST
SRC.CTAG0.MAKE_TEKRAR =
SRC.SYSMESSAGE @38,,1,1  Tekrar işleminiz sona erdi.
SRC.CTAG.Craft_Menu.Notice = <DEF.BFONT_LRED>Tekrar işleminiz sona erdi.
SRC.EVENTS -E_MAKELAST
IF (<ISDIALOGOPEN D_CRAFT_MENU>)
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
ENDIF


ON=@SKILLFAIL
LOCAL.MAKEITEM=<TAG0.CRAFT_LASTMAKE_BASEID>

CTAG0.MAKE_TEKRAR -=1
SYSMESSAGE @,,1,1  Kalan tekrar: <DCTAG0.MAKE_TEKRAR>/<dCTAG0.MAKE_TEKRAR_BASE>
CTAG.Craft_Menu.Notice = <DEF.BFONT_WHITE>Kalan tekrar: <DCTAG0.MAKE_TEKRAR>/<dCTAG0.MAKE_TEKRAR_BASE>
TIMERF 1,MAKEITEM <LOCAL.MAKEITEM>

IF (<ISDIALOGOPEN D_CRAFT_MENU>)
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
ENDIF






ON=@SKILLABORT
LOCAL.MAKEITEM=<TAG0.CRAFT_LASTMAKE_BASEID>
CTAG0.MAKE_TEKRAR -=1
SYSMESSAGE @,,1,1  Kalan tekrar: <DCTAG0.MAKE_TEKRAR>/<dCTAG0.MAKE_TEKRAR_BASE>
CTAG.Craft_Menu.Notice = <DEF.BFONT_WHITE>Kalan tekrar: <DCTAG0.MAKE_TEKRAR>/<dCTAG0.MAKE_TEKRAR_BASE>
TIMERF 1,MAKEITEM <LOCAL.MAKEITEM>
IF (<ISDIALOGOPEN D_CRAFT_MENU>)
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
ENDIF




ON=@SKILLMAKEITEM
LOCAL.MAKEITEM=<SRC.TAG0.CRAFT_LASTMAKE_BASEID>

SRC.CTAG0.MAKE_TEKRAR --

IF (<SRC.DCTAG0.MAKE_TEKRAR> <= 0)
	SRC.SYSMESSAGE @38,,1,1  Tekrar işleminiz sona erdi.
	SRC.CTAG.Craft_Menu.Notice = <DEF.BFONT_LRED>Tekrar işleminiz sona erdi.
	SRC.EVENTS -E_MAKELAST
	SRC.CTAG0.MAKE_TEKRAR=

ELIF !(<SRC.CANMAKE <LOCAL.MAKEITEM>>)
	SRC.SYSMESSAGE @38,,1,1  <SERV.ITEMDEF.<LOCAL.MAKEITEM>.NAME> uretebilecek malzemeniz veya yeteneğiniz bulunmuyor.
	SRC.CTAG.Craft_Menu.Notice = <DEF.BFONT_LRED><SERV.ITEMDEF.<LOCAL.MAKEITEM>.NAME> uretebilecek malzemeniz veya yeteneğiniz bulunmuyor.
	SRC.CTAG0.MAKE_TEKRAR=
	SRC.EVENTS -E_MAKELAST
ENDIF

SRC.SYSMESSAGE @,,1,1  Kalan tekrar: <DCTAG0.MAKE_TEKRAR>/<dCTAG0.MAKE_TEKRAR_BASE>
SRC.CTAG.Craft_Menu.Notice = <DEF.BFONT_WHITE>Kalan tekrar: <DCTAG0.MAKE_TEKRAR>/<dCTAG0.MAKE_TEKRAR_BASE>
SRC.TIMERF 1,MAKEITEM <LOCAL.MAKEITEM>

IF (<ISDIALOGOPEN D_CRAFT_MENU>)
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
ENDIF



// SCROLL YAZMA GEREKSINIM KONTROLU
[FUNCTION ISCANCAST]
IF !(<ISEMPTY <ARGS>>)
	IF (STRMATCH('I_SCROLL_PARALYZE_FIELD','<ARGS>'))
		LOCAL.SPELL=S_PARALYZATION_FIELD
	ELSE
		LOCAL.SPELL=S_<STRSUB 9 99 <ARGS>>
	ENDIF
	IF (<CANCAST <LOCAL.SPELL>>)	
		RETURN 1
	ENDIF
	RETURN 0
ENDIF


[DEFNAME CRAFT_MENU_DEFS]
// ALCHEMY
CRAFT_SKILL_ALCHEMY_ITEMLIST i_potion_bleed,I_POTION_HEALLESS,I_POTION_HEAL,I_POTION_HEALGREAT,I_POTION_CURELESS,I_POTION_CURE,I_POTION_CUREGREAT,I_POTION_REFRESH,I_POTION_REFRESHTOTAL,I_POTION_STRENGTH,I_POTION_STRENGTHGREAT,I_POTION_AGILITY,I_POTION_AGILITYGREAT,I_POTION_EXPLOSIONLESS,I_POTION_EXPLOSION,I_POTION_EXPLOSIONGREAT,I_POTION_POISONLESS,I_POTION_POISON,I_POTION_POISONGREAT,I_POTION_POISONDEADLY,I_POTION_MANA,I_POTION_MANATOTAL,I_POTION_NIGHTSIGHT

// BOWCRAFT
CRAFT_SKILL_BOWCRAFT_ITEMLIST I_KINDLING,I_ARROW_SHAFT,I_ARROW,I_XBOLT,I_BOW,I_CROSSBOW,I_CROSSBOW_HEAVY

// CARPENTRY
CRAFT_SKILL_CARPENTRY_ITEMLIST I_CHAIR_THRONE,I_CHAIR_WOOD_ROUGH_DARK,I_CHAIR_WOOD_FANCY_CUSHION,I_CHAIR_WOOD_CUSHION,I_CHAIR_STOOL_BAR,I_CHAIR_STOOL,I_BENCH_WOOD_DARK, I_ARMOIR_DK,I_ARMOIR_LT,I_ARMOIRE_CHERRY,I_ARMOIRE_MAPLE,I_CABINET_SHORT,I_CABINET_TALL,I_WOODEN_CHEST_FINISHED,I_WOODEN_FOOT_LOCKER,I_WOODEN_CHEST_GILDED,I_WOODEN_CHEST_ORNATE,I_WOODEN_CHEST_PLAIN,I_ARMOIRE_ELEGANT,I_ARMOIRE_RED,I_CHEST_WOODEN_BRASS,I_BOX_WOOD,I_CRATE_SMALL,I_CRATE_MD,I_CRATE_LG, I_TABLE_LOW1,I_TABLE_LOW2,I_TABLE_NIGHTSTAND,I_TABLE_WOOD_ROUGH_SMALL,I_TABLE_WOOD_ROUGH_BOARD,I_TABLE_WRITING, I_CLUB,I_STAFF_GNARLED,I_STAFF_QUARTER,I_SHIELD_WOOD,I_SHEPHERDS_CROOK,I_FISHING_POLE, I_BAR_DOOR_NE,I_BAR_PIECE,I_BOOKCASE,I_BOOKCASE_FULL,I_BUREAU_2,I_MUSIC_STAND_TALL,I_DRUM,I_LYRE,I_LUTE,I_HARP_LARGE,I_TAMBOURINE,I_TAMBOURINE_TAS,I_SCROLL_BLANK,I_TROPHY_MOUNTBOARD_LG,I_MODEL_SHIP,I_BARREL_LID,I_DRESS_FORM,I_EASEL,I_BULLETIN_BOARD,I_DARTBOARD,I_BARREL_OPEN,I_MUSIC_STAND,I_EASEL_WITH_CANVAS,I_SADDLE

// CARTOGRAPHY
CRAFT_SKILL_CARTOGRAPHY_ITEMLIST I_MAP_LOCAL,I_MAP_CITY,I_MAP_SEA_CHART,I_MAP_WORLD

// INSCRIPTION
CRAFT_SKILL_INSCRIPTION_ITEMLIST I_RUNEBOOK,I_FULL_SPELLBOOK,I_SCROLL_CLUMSY,I_SCROLL_CREATE_FOOD,I_SCROLL_FEEBLEMIND,I_SCROLL_HEAL,I_SCROLL_MAGIC_ARROW,I_SCROLL_NIGHT_SIGHT,I_SCROLL_REACTIVE_ARMOR,I_SCROLL_WEAKEN, I_SCROLL_AGILITY,I_SCROLL_CUNNING,I_SCROLL_CURE,I_SCROLL_HARM,I_SCROLL_PROTECTION,I_SCROLL_STRENGTH, I_SCROLL_BLESS,I_SCROLL_FIREBALL,I_SCROLL_POISON,I_SCROLL_TELEPORT,I_SCROLL_WALL_OF_STONE, I_SCROLL_ARCHCURE,I_SCROLL_ARCHPROTECTION,I_SCROLL_CURSE,I_SCROLL_FIRE_FIELD,I_SCROLL_GREATER_HEAL,I_SCROLL_LIGHTNING,I_SCROLL_MANA_DRAIN,I_SCROLL_RECALL, I_SCROLL_BLADE_SPIRITS,I_SCROLL_DISPEL_FIELD,,I_SCROLL_MAGIC_REFLECTION,I_SCROLL_MIND_BLAST,I_SCROLL_PARALYZE,I_SCROLL_POISON_FIELD,I_SCROLL_SUMMON_CREATURE, I_SCROLL_DISPEL,I_SCROLL_ENERGY_BOLT,I_SCROLL_EXPLOSION,I_SCROLL_INVISIBILITY,I_SCROLL_MARK,I_SCROLL_MASS_CURSE,I_SCROLL_PARALYZE_FIELD,I_SCROLL_REVEAL, I_SCROLL_CHAIN_LIGHTNING,I_SCROLL_ENERGY_FIELD,I_SCROLL_FLAMESTRIKE,I_SCROLL_GATE_TRAVEL,I_SCROLL_MANA_VAMPIRE,I_SCROLL_MASS_DISPEL,I_SCROLL_METEOR_SWARM, I_SCROLL_EARTHQUAKE,I_SCROLL_ENERGY_VORTEX,I_SCROLL_RESURRECTION,I_SCROLL_SUMMON_ELEM_AIR,I_SCROLL_SUMMON_DAEMON,I_SCROLL_SUMMON_ELEM_EARTH,I_SCROLL_SUMMON_ELEM_FIRE,I_SCROLL_SUMMON_ELEM_WATER

// TAILOR
CRAFT_SKILL_TAILORING_ITEMLIST I_LEATHER_BOOTS_WYRM,I_LEATHER_CAP_WYRM,I_LEATHER_SLEEVES_WYRM,I_LEATHER_GORGET_WYRM,I_LEATHER_LEGGINGS_WYRM,I_LEATHER_TUNIC_WYRM,I_LEATHER_GLOVES_WYRM,I_SHROUD,I_MAGE_ROBE,I_ROBE_FIRE,I_ROBE_WATER,I_ROBE_VIRTUOUS,I_ROBE_PREDATOR,I_ROBE_MYTHICAL_DARK,I_ROBE_PALADIN,I_ROBE_HUMAN_MAGE,I_ROBE_HUMAN_WARRIOR,I_ROBE_ELF_MAGE,I_ROBE_ELF_WARRIOR,I_BOOTS_CALF,I_BOOTS_THIGH,I_SHOES_PLAIN,I_SANDALS,I_BANDANA,I_BONNET,I_CAP,I_HAT_WIZARDS,I_HAT_FEATHER,I_HAT_FLOPPY,I_HAT_WIDE_BRIM,I_HAT_JESTER,I_HAT_TRICORNE,I_SKULL_CAP,I_HAT_STRAW,I_HAT_STRAW_TALl,I_KILT,I_PANTS_LONG,I_PANTS_SHORT,I_SKIRT_LONG, I_CAPE,I_DOUBLET,I_DRESS_FULL,I_SHIRT_FANCY,I_JESTER_SUIT,I_DRESS_PLAIN,I_ROBE,I_SHIRT_PLAIN,I_SURCOAT,I_TUNIC, I_LEATHER_GLOVES,I_LEATHER_GORGET,I_LEATHER_LEGGINGS,I_LEATHER_SLEEVES,I_LEATHER_TUNIC,I_LEATHER_CAP, I_STUDDED_GLOVES,I_STUDDED_GORGET,I_STUDDED_LEGGINGS,I_STUDDED_SLEEVES,I_STUDDED_TUNIC,I_CURTAIN_FREESTANDING,I_CURTAIN_BANNER_NW,I_MASK_BEAR,I_MASK_DEER,I_MASK_BLUE,I_MASK_TRIBAL_RED,I_BASKET_SQUARE,I_BACKPACK,I_BAG,I_SASH,I_APRON_FULL,I_APRON_HALF

// TINKERING
CRAFT_SKILL_TINKERING_ITEMLIST I_BRACELET_GOLD,I_EARRINGS_GOLD,I_NECKLACE_GOLD_BEADS,I_NECKLACE_GOLD,I_NECKLACE_METAL_BEADS,I_RING_GOLD,I_NAILS,I_HINGE,I_BARREL_TAP,I_CLOCK_PARTS,I_GEARS,I_SEXTANT_PARTS,I_SPRINGS,I_AXEL_AND_GEARS,I_AXEL,I_SAW,I_HATCHET,I_SEWING_KIT,I_SCISSORS,I_MORTAR_PESTLE,I_LOCKPICK,I_HAMMER_SMITH,I_PICKAXE,I_SHOVEL,I_GOBLET,I_KNIFE_SKINNING,I_PLATE,I_CLEAVER_MEAT,I_MUG_PEWTER,I_SPOON,I_FORK,I_KNIFE,I_KEY_COPPER,I_SEXTANT,I_POUCH_TRAPPED,I_CANDLEABRA,I_HEATING_STAND,I_LANTERN,I_CLOCK,I_GLOBE,I_CHEST_METAL,I_SCALES,I_CANDLE

CRAFT_SKILL_BLACKSMITHING_ITEMLIST I_DARK_ORACS,I_DARK_PIKE,I_HALBERD_SILVER,I_MACE_WAR_SILVER,I_WAR_FORK_SILVER,I_KATANA_SILVER,I_SWORD_VIKING_SILVER,I_KRYSS_SILVER,I_WAR_CLEAVER_SILVER,I_AXE_BATTLE_LARGE_SILVER,I_STAFF_BLADED_DOUBLE_SILVER,I_BONE_HARVESTER_SILVER,I_MACE_DIAMOND_POISON,I_WAR_CLEAVER_POISON,I_STAFF_BLADED_VIRTUOUS,I_VIRTUOUS_SWORD_VIKING,I_VIRTUOUS_SWORD_LONG,I_VIRTUOUS_SWORD_BROAD,I_VIRTUOUS_KATANA,I_VIRTUOUS_SCIMITAR,I_VIRTUOUS_CUTLASS,I_VIRTUOUS_HALBERD,I_VIRTUOUS_BARDICHE,I_SCYTHE_VIRTUOUS,I_SILVER_PLATEMAIL,I_SILVER_PLATEMAIL_ARMS,I_SILVER_PLATEMAIL_LEGGINGS,I_SILVER_PLATEMAIL_GORGET,I_SILVER_PLATEMAIL_GAUNTLETS,I_SILVER_BUCKLER_SHIELD,I_SILVER_HEATER_SHIELD,I_SILVER_METAL_SHIELD,I_SILVER_PLATE_HELM,I_SILVER_CLOSED_HELM,I_SILVER_HELMET,I_SILVER_BASCINET,I_SILVER_NOSE_HELM,,I_VIRTUOUS_PLATE_HELM,I_VIRTUOUS_HELM_CLOSED,I_VIRTUOUS_BASCINET,I_VIRTUOUS_HELM_NOSE,I_VIRTUOUS_PLATEMAIL_CHEST,I_VIRTUOUS_PLATEMAIL_ARMS,I_VIRTUOUS_PLATEMAIL_LEGGINGS,I_VIRTUOUS_PLATEMAIL_GORGET,I_VIRTUOUS_PLATEMAIL_GAUNTLETS,I_VIRTUOUS_HEATER_SHIELD,I_EMPIRE_PLATEMAIL_CHEST,I_EMPIRE_PLATEMAIL_ARMS,I_EMPIRE_PLATEMAIL_LEGGINGS,I_EMPIRE_PLATEMAIL_GORGET,I_EMPIRE_PLATEMAIL_GAUNTLETS,I_EMPIRE_HEATER_SHIELD,I_EMPIRE_PLATE_HELM,I_EMPIRE_CLOSED_HELM,I_EMPIRE_BASCINET,I_EMPIRE_NOSE_HELM,I_ORIEL_PLATEMAIL,I_ORIEL_PLATEMAIL_ARMS,I_ORIEL_PLATEMAIL_LEGGINGS,I_ORIEL_PLATEMAIL_GORGET,I_ORIEL_PLATEMAIL_GAUNTLETS,I_ORIEL_HEATER_SHIELD,I_ORIEL_PLATE_HELM,I_ORIEL_CLOSED_HELM,I_ORIEL_BASCINET,I_ORIEL_NOSE_HELM,I_FATAL_PLATEMAIL,I_FATAL_PLATEMAIL_ARMS,I_FATAL_PLATEMAIL_LEGGINGS,I_FATAL_PLATEMAIL_GORGET,I_FATAL_HEATER_SHIELD,I_FATAL_PLATEMAIL_GAUNTLETS,I_FATAL_PLATE_HELM,I_FATAL_CLOSED_HELM,I_FATAL_BASCINET,I_FATAL_NOSE_HELM,I_PLATEMAIL_CHEST,I_PLATEMAIL_GLOVES,I_PLATEMAIL_GORGET,I_PLATEMAIL_LEGGINGS,I_PLATEMAIL_ARMS, I_SHIELD_ROUND_BRONZE,I_SHIELD_BUCKLER,I_SHIELD_KITE_METAL,I_SHIELD_HEATER,I_SHIELD_ROUND_METAL, I_PLATEMAIL_HELM,I_HELM_CLOSED,I_HELM_OPEN,I_BASCINET,I_HELM_NOSE, I_AXE,I_AXE_BATTLE,I_AXE_DOUBLE,I_AXE_TWO_HAND,I_AXE_BATTLE_LARGE,I_AXE_WAR,I_MACE,I_MAUL,I_HAMMER_WAR,I_MACE_WAR,I_KNIFE_BUTCHER,I_DAGGER,I_KNIFE_SKINNING,I_BARDICHE,I_HALBERD,I_PITCHFORK,I_SPEAR_SHORT,I_SPEAR,I_WAR_FORK,I_CUTLASS,I_SWORD_BROAD,I_KATANA,I_KRYSS,I_SWORD_LONG,I_SCIMITAR,I_SWORD_VIKING, I_SHADOW_PLATEMAIL_CHEST,I_SHADOW_PLATEMAIL_ARMS,I_SHADOW_PLATEMAIL_LEGGINGS,I_SHADOW_PLATEMAIL_GORGET,I_SHADOW_PLATEMAIL_GAUNTLETS,I_SHADOW_BUCKLER_SHIELD,I_SHADOW_HEATER_SHIELD,I_SHADOW_METAL_SHIELD,I_SHADOW_PLATE_HELM,I_SHADOW_CLOSED_HELM,I_SHADOW_HELMET,I_SHADOW_BASCINET,I_SHADOW_NOSE_HELM, I_AGAPITE_PLATEMAIL_CHEST,I_AGAPITE_PLATEMAIL_ARMS,I_AGAPITE_PLATEMAIL_LEGGINGS,I_AGAPITE_PLATEMAIL_GORGET,I_AGAPITE_PLATEMAIL_GAUNTLETS,I_AGAPITE_BUCKLER_SHIELD,I_AGAPITE_HEATER_SHIELD,I_AGAPITE_METAL_SHIELD,I_AGAPITE_PLATE_HELM,I_AGAPITE_CLOSED_HELM,I_AGAPITE_HELMET,I_AGAPITE_BASCINET,I_AGAPITE_NOSE_HELM, I_MYTHERIL_PLATEMAIL_CHEST,I_MYTHERIL_PLATEMAIL_ARMS,I_MYTHERIL_PLATEMAIL_LEGGINGS,I_MYTHERIL_PLATEMAIL_GORGET,I_MYTHERIL_PLATEMAIL_GAUNTLETS, I_MYTHERIL_BUCKLER_SHIELD,I_MYTHERIL_HEATER_SHIELD,I_MYTHERIL_METAL_SHIELD,I_MYTHERIL_PLATE_HELM,I_MYTHERIL_CLOSED_HELM,I_MYTHERIL_HELMET,I_MYTHERIL_BASCINET,I_MYTHERIL_NOSE_HELM, I_VERITE_PLATEMAIL_CHEST,I_VERITE_PLATEMAIL_ARMS,I_VERITE_PLATEMAIL_LEGGINGS,I_VERITE_PLATEMAIL_GORGET,I_VERITE_PLATEMAIL_GAUNTLETS,I_VERITE_BUCKLER_SHIELD,I_VERITE_HEATER_SHIELD,I_VERITE_METAL_SHIELD,I_VERITE_PLATE_HELM,I_VERITE_CLOSED_HELM,I_VERITE_HELMET,I_VERITE_BASCINET,I_VERITE_NOSE_HELM, I_VALORITE_PLATEMAIL_CHEST,I_VALORITE_PLATEMAIL_ARMS,I_VALORITE_PLATEMAIL_LEGGINGS,I_VALORITE_PLATEMAIL_GORGET,I_VALORITE_PLATEMAIL_GAUNTLETS,I_VALORITE_BUCKLER_SHIELD,I_VALORITE_HEATER_SHIELD,I_VALORITE_METAL_SHIELD,I_VALORITE_PLATE_HELM,I_VALORITE_CLOSED_HELM,I_VALORITE_HELMET,I_VALORITE_BASCINET,I_VALORITE_NOSE_HELM, I_BLACKROCK_PLATEMAIL_CHEST,I_BLACKROCK_PLATEMAIL_ARMS,I_BLACKROCK_PLATEMAIL_LEGGINGS,I_BLACKROCK_PLATEMAIL_GORGET,I_BLACKROCK_PLATEMAIL_GAUNTLETS,I_BLACKROCK_BUCKLER_SHIELD,I_BLACKROCK_HEATER_SHIELD,I_BLACKROCK_METAL_SHIELD,I_BLACKROCK_PLATE_HELM,I_BLACKROCK_CLOSED_HELM,I_BLACKROCK_HELMET,I_BLACKROCK_BASCINET,I_BLACKROCK_NOSE_HELM, I_BLOODROCK_PLATEMAIL_CHEST,I_BLOODROCK_PLATEMAIL_ARMS,I_BLOODROCK_PLATEMAIL_LEGGINGS,I_BLOODROCK_PLATEMAIL_GORGET,I_BLOODROCK_PLATEMAIL_GAUNTLETS,I_BLOODROCK_BUCKLER_SHIELD,I_BLOODROCK_HEATER_SHIELD,I_BLOODROCK_METAL_SHIELD,I_BLOODROCK_PLATE_HELM,I_BLOODROCK_CLOSED_HELM,I_BLOODROCK_HELMET,I_BLOODROCK_BASCINET,I_BLOODROCK_NOSE_HELM



[FUNCTION Craft_Menu_Rebuild_All]	
Craft_Menu_Rebuild_Single Alchemy
Craft_Menu_Rebuild_Single Bowcraft
Craft_Menu_Rebuild_Single Carpentry
Craft_Menu_Rebuild_Single Cartography
Craft_Menu_Rebuild_Single Inscription
Craft_Menu_Rebuild_Single Tailoring
Craft_Menu_Rebuild_Single Blacksmithing
Craft_Menu_Rebuild_Single Tinkering



[FUNCTION Craft_Menu_Rebuild_Single]	
DB.CONNECT
IF (<DB.CONNECTED>)	
	LOCAL.SKILLNAME <STRTOUPPER <ARGS>>
	
	// db.EXECUTE DROP TABLE IF EXISTS craftitems
	// DB.EXECUTE CREATE TABLE IF NOT EXISTS craftitems ('id' INTEGER PRIMARY KEY AUTOINCREMENT NOT NULL, 'baseid' TEXT, 'name' TEXT, 'type' TEXT, 'category' TEXT, 'subsection' TEXT, 'description' TEXT, 'skill_key' TEXT, 'skill_val' TEXT, 'created_at' TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP, 'updated_at' TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP)


	// DB.EXECUTE "DROP TABLE IF EXISTS craftitems";
			
	DB.EXECUTE CREATE TABLE IF NOT EXISTS craftitems (id INTEGER PRIMARY KEY AUTO_INCREMENT NOT NULL, baseid VARCHAR(255), name VARCHAR(255), type VARCHAR(255), category VARCHAR(255), subsection VARCHAR(255), description VARCHAR(255), skill_key VARCHAR(255), skill_val VARCHAR(255), skill_name VARCHAR(255), created_at DATETIME NOT NULL DEFAULT current_timestamp, updated_at DATETIME NOT NULL DEFAULT current_timestamp)


	DB.EXECUTE BEGIN
				
	// 
	FOR 1 <ARRAYCOUNT <DEF.CRAFT_SKILL_<LOCAL.SKILLNAME>_ITEMLIST>>
		LOCAL.ITEM=<ARRAY <DEF.CRAFT_SKILL_<LOCAL.SKILLNAME>_ITEMLIST>,<DLOCAL._FOR>>
		
		IF (<SERV.ITEMDEF.<LOCAL.ITEM>>)
			LOCAL.SKILL_KEY=<SERV.ITEMDEF.<LOCAL.ITEM>.SKILLMAKE.1.KEY>
			LOCAL.SKILL_VAL=<SERV.ITEMDEF.<LOCAL.ITEM>.SKILLMAKE.1.VAL>
			
			LOCAL.CATEGORY=<StrReplace <SERV.ITEMDEF.<LOCAL.ITEM>.CATEGORY>>
			LOCAL.SUBSECTION=<StrReplace <SERV.ITEMDEF.<LOCAL.ITEM>.SUBSECTION>>
			LOCAL.DESCRIPTION=<StrReplace <SERV.ITEMDEF.<LOCAL.ITEM>.DESCRIPTION>>
			
			DB.QUERY "SELECT id FROM craftitems WHERE baseid='<STRTOUPPER <LOCAL.ITEM>>'"
			
			IF (<DB.ROW.NUMROWS>)
				// DB.EXECUTE "UPDATE craftitems SET name='<StrReplace <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>', type='<STRTOUPPER <SERV.ITEMDEF.<LOCAL.ITEM>.TYPE>>', category='<LOCAL.CATEGORY>', subsection='<LOCAL.subsection>', description='<LOCAL.description>', skill_key='<STRTOUPPER <LOCAL.SKILL_KEY>>', skill_val='<FVAL <DLOCAL.SKILL_VAL>>' WHERE baseid='<STRTOUPPER <LOCAL.ITEM>>'"
				DB.EXECUTE "UPDATE craftitems SET name='<StrReplace <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>', type='<STRTOUPPER <SERV.ITEMDEF.<LOCAL.ITEM>.TYPE>>', category='<LOCAL.CATEGORY>', skill_key='<STRTOUPPER <LOCAL.SKILL_KEY>>', skill_val='<FVAL <DLOCAL.SKILL_VAL>>', skill_name='<LOCAL.SKILLNAME>' WHERE baseid='<STRTOUPPER <LOCAL.ITEM>>'"
			ELSE
				// DB.EXECUTE INSERT INTO craftitems (baseid,  name , type , category , subsection , description , skill_key , skill_val, skill_name) VALUES ('<STRTOUPPER <LOCAL.ITEM>>','<StrReplace <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>','<STRTOUPPER <SERV.ITEMDEF.<LOCAL.ITEM>.TYPE>>','<LOCAL.CATEGORY>','<LOCAL.SUBSECTION>','<LOCAL.DESCRIPTION>','<STRTOUPPER <LOCAL.SKILL_KEY>>','<FVAL <DLOCAL.SKILL_VAL>>','<LOCAL.SKILLNAME>')
				DB.EXECUTE INSERT INTO craftitems (baseid,  name , type , category ,  skill_key , skill_val, skill_name) VALUES ('<STRTOUPPER <LOCAL.ITEM>>','<StrReplace <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>','<STRTOUPPER <SERV.ITEMDEF.<LOCAL.ITEM>.TYPE>>','<LOCAL.CATEGORY>','<STRTOUPPER <LOCAL.SKILL_KEY>>','<FVAL <DLOCAL.SKILL_VAL>>','<LOCAL.SKILLNAME>')
			ENDIF
			
		ENDIF
	ENDFOR

	DB.EXECUTE COMMIT

DB.CLOSE
ENDIF




[FUNCTION CraftSkill]
CTAG.Craft_Menu.Skill=<STRTOUPPER <argv1>>

CTAG.Craft_Menu.Item_WHERE="id"
CTAG.Craft_Menu.Item_INDEX=0
CTAG.Craft_Menu.Item_ORDER="name ASC"

CTAG.Craft_Menu.Cat_WHERE="id"
CTAG.Craft_Menu.Cat_INDEX=0

CTAG.Craft_Menu.Sub_WHERE="id"
CTAG.Craft_Menu.Sub_INDEX=0

CTAG.Craft_Menu.Notice=

// emülatör zaten dialogu açıyor
 DIALOGCLOSE d_craft_menu
// DIALOG d_craft_menu
// SYSMESSAGE <STRTOUPPER <argv1>>




[FUNCTION TOOLS]
SERV.NEWITEM I_MORTAR_PESTLE,1,<FINDLAYER.21>,1
SERV.NEWITEM I_ARROW_SHAFT,1,<FINDLAYER.21>,1
SERV.NEWITEM I_SAW,1,<FINDLAYER.21>,1

SERV.NEWITEM I_MAPMAKERS_PEN,1,<FINDLAYER.21>,1

SERV.NEWITEM I_MAP_BLANK,100,<FINDLAYER.21>,1

SERV.NEWITEM I_SCROLL_BLANK,100,<FINDLAYER.21>,1
SERV.NEWITEM I_SEWING_KIT,1,<FINDLAYER.21>,1
SERV.NEWITEM I_TINKER_TOOLS,1,<FINDLAYER.21>,1
SERV.NEWITEM I_BOTTLE_EMPTY,100,<FINDLAYER.21>,1
SERV.NEWITEM i_hammer_smith,1,<FINDLAYER.21>,1

SERV.NEWITEM I_BAG,1,<FINDLAYER.21>,1
REF1=<NEW>

FOR 1 <ARRAYCOUNT <DEF.STORE_BOX_ITEMLIST_INGOTS>>
LOCAL.ITEM <ARRAY <DEF.STORE_BOX_ITEMLIST_INGOTS>,<DLOCAL._FOR>>

SERV.NEWITEM <LOCAL.ITEM>,100,<REF1>,1
ENDFOR


[DIALOG d_craft_menu]
<DEF.DSP>
// RESIZE 0,0,515,505
RESIZEPIC 0 0 5054 620 460
RESIZEPIC 10 10 5054 600 440

DHTMLGUMP 10 10 600 25 0 0 <def.center><def.bfont_white><serv.name> <SRC.CTAG.Craft_Menu.Skill> Menu

// Sol
DORIGIN 20 30
resizepic +0 +20 5054 170 275


DB.CONNECT
IF (<DB.CONNECTED>)
	DB.QUERY "SELECT id, category FROM craftitems WHERE skill_name='<SRC.CTAG.Craft_Menu.Skill>' AND <SRC.CTAG.Craft_Menu.Cat_WHERE> GROUP BY category ORDER BY category ASC LIMIT <SRC.DCTAG.Craft_Menu.Cat_INDEX>,9"
	
	IF (<DB.ROW.NUMROWS>)
		FOR cats 0 <EVAL <DB.ROW.NUMROWS>-1>
			IF (<DB.ROW.<DLOCAL.cats>.id>)
				LOCAL.CATCOUNT=<DB.ROW.NUMROWS>
				IF (<ISEMPTY <DB.ROW.<DLOCAL.cats>.category>>)
					DHTMLGUMP +10 <EVAL (<LOCAL.cats>*25)+60> 140 25 0 0 <def.bfont_white>Tümü
				ELSE
					DHTMLGUMP +10 <EVAL (<LOCAL.cats>*25)+60> 140 25 0 0 <def.bfont_white><DB.ROW.<DLOCAL.cats>.category>
				ENDIF
				BUTTON 170 <EVAL (<LOCAL.cats>*25)+65> 5601 5605 1 0 <EVAL <DB.ROW.<DLOCAL.cats>.id>+100>
			ENDIF			
		ENDFOR
	ENDIF
	
	DTEXT +10 +270 38 Sayfa: <EVAL (<SRC.dCTAG.Craft_Menu.Cat_INDEX>/9)+1>
	IF (<EVAL (<LOCAL.CATCOUNT>/9)>)
		DBUTTON 170,295,015a4,015a5,1,0,1000 // ++ 
	ENDIF
	DBUTTON 135,295,015a1,015a3,1,0,1001 // --
ENDIF
DB.CLOSE
DTEXT +0 +0 <DEF.SMSG_CUSTOM_COLOR> Kategori (<LOCAL.CATCOUNT>) ;




// Alt
// Notice
DORIGIN 20 325
IF !(<ISEMPTY <SRC.CTAG.Craft_Menu.Notice>>)
	DHTMLGUMP +0 +10 300 105 0 1 <DEF.CENTER><def.br><def.br><SRC.CTAG.Craft_Menu.Notice>
ELSE
	DHTMLGUMP +0 +10 300 105 0 1 Not:
ENDIF

// Bilgiler
DORIGIN 330 325
// RESIZEPIC +0 +0 0BB8 220 120
DB.CONNECT

// IF (<DB.CONNECTED>)
	// DB.EXECUTE CREATE TABLE IF NOT EXISTS '<account.name>' ('id' INTEGER PRIMARY KEY AUTOINCREMENT NOT NULL, 'item_baseid' TEXT, item_count INT, 'item_quality' INT, 'char_uid' TEXT, 'skill' TEXT, 'created_at' TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP,  'updated_at' TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP)

	// DB.QUERY "SELECT * FROM '<SRC.ACCOUNT.NAME>'"
	// LOCAL.MAKEITEMCOUNT=<DB.ROW.NUMROWS>
	
	// DB.QUERY "SELECT * FROM '<SRC.ACCOUNT.NAME>' WHERE item_quality >= 175"
	// LOCAL.MAKEHQITEMCOUNT=<DB.ROW.NUMROWS>

// ENDIF
// DB.CLOSE

DHTMLGUMP +5 +10 260 25 0 0 <def.bfont_white>Toplam üretilen eşya: <QVAL (<LOCAL.MAKEITEMCOUNT>)?<DEF.BFONT_CYAN><NumericFormat <LOCAL.MAKEITEMCOUNT>>:<DEF.BFONT_RED>Yok>
DHTMLGUMP +5 +30 260 25 0 0 <def.bfont_white>Toplam üretilen kaliteli eşya: <QVAL (<LOCAL.MAKEHQITEMCOUNT>)?<DEF.BFONT_CYAN><NumericFormat <LOCAL.MAKEHQITEMCOUNT>>:<DEF.BFONT_RED>Yok> 
DHTMLGUMP +5 +50 260 25 0 0 <def.bfont_white>Son üretilen: <QVAL (<SRC.TAG0.CRAFT_LASTMAKE_BASEID>)?<DEF.BFONT_CYAN><SERV.ITEMDEF.<SRC.TAG0.CRAFT_LASTMAKE_BASEID>.NAME>:<DEF.BFONT_RED>Yok>
DHTMLGUMP +5 +70 260 25 0 0 <def.bfont_white><SRC.CTAG.Craft_Menu.Skill> : <SRC.<SERV.SKILL.<SRC.CTAG.Craft_Menu.Skill>.KEY>>

// BUTTON +140 +100 5601 5605 1 0 200001
// DHTMLGUMP +45 +97 100 25 0 0 <def.bfont_white>İmza:


BUTTON +240 +100 5601 5605 1 0 200000 
DHTMLGUMP +145 +97 100 25 0 0 <def.bfont_white>Tekrar Yap


// Orta
DORIGIN 195 20
RESIZEPIC +0 +30 5054 390 275


DB.CONNECT "db/main.db"

IF (<DB.CONNECTED>)
	DB.QUERY "SELECT id, baseid FROM craftitems WHERE <SRC.CTAG.Craft_Menu.Item_WHERE> AND skill_name='<SRC.CTAG.Craft_Menu.Skill>' ORDER BY name DESC LIMIT <SRC.dCTAG.Craft_Menu.Item_INDEX>,9"

	IF (<DB.ROW.NUMROWS>)
		LOCAL.ITEMCOUNT=<DB.ROW.NUMROWS>
		LOCAL.X=210
		LOCAL.Y=15
		
		FOR items 0 <EVAL <DB.ROW.NUMROWS>-1>
			IF (<DB.ROW.<DLOCAL.items>.id>)
				LOCAL.ITEM <DB.ROW.<DLOCAL.items>.baseid>
				LOCAL.Y +=80
				
				IF !(<SRC.CANMAKESKILL <LOCAL.ITEM>>)
					BUTTON <EVAL (<DLOCAL.X>)> <EVAL (<DLOCAL.Y>-10)> 210 211 1 0 <EVAL <DB.ROW.<DLOCAL.items>.id>+10000>
					DCROPPEDTEXT <EVAL (<DLOCAL.X>)> <EVAL (<DLOCAL.Y>-30)> 90 25 38 <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>
				ELSEIF !(<SRC.CANMAKE <LOCAL.ITEM>>)
					BUTTON <EVAL (<DLOCAL.X>)> <EVAL (<DLOCAL.Y>-10)> 210 211 1 0 <EVAL <DB.ROW.<DLOCAL.items>.id>+10000>
					DCROPPEDTEXT <EVAL (<DLOCAL.X>)> <EVAL (<DLOCAL.Y>-30)> 90 25 38 <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>
				ELSE
					BUTTON <EVAL (<DLOCAL.X>)> <EVAL (<DLOCAL.Y>-10)> 210 211 1 0 <EVAL <DB.ROW.<DLOCAL.items>.id>+10000>
					DCROPPEDTEXT <EVAL (<DLOCAL.X>)> <EVAL (<DLOCAL.Y>-30)> 90 25 044 <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>
				ENDIF	
				
				TILEPICHUE <EVAL (<DLOCAL.X>)+50> <EVAL (<DLOCAL.Y>+10)> <SERV.ITEMDEF.<DB.ROW.<DLOCAL.items>.baseid>.ID> <DSERV.ITEMDEF.<DB.ROW.<DLOCAL.items>.baseid>.RESDISPDNHUE>
				BUTTON <EVAL (<DLOCAL.X>)+20> <EVAL (<DLOCAL.Y>-10)> 0845 0846 1 0 <EVAL <DB.ROW.<DLOCAL.items>.id>+50000>
				
				IF (<LOCAL.Y> > 240)
					LOCAL.X +=140
					LOCAL.Y=15
				ENDIF
			
				
			ENDIF			
		ENDFOR
	ENDIF

	DTEXT +10 +280 38 Sayfa: <EVAL (<SRC.dCTAG.Craft_Menu.Item_INDEX>/9)+1> 

	IF (<EVAL (<LOCAL.ITEMCOUNT>/9)>)
		DBUTTON 560,300,015a4,015a5,1,0,100000 // ++ 
	ENDIF
	DBUTTON 530,300,015a1,015a3,1,0,100001 // --
	
	DTEXT +0 +10 <DEF.SMSG_CUSTOM_COLOR> Kategori İçeriği (<LOCAL.ITEMCOUNT>) ;

ENDIF
DB.CLOSE

[DIALOG D_CRAFT_MENU BUTTON]
//// KATEGORI BUTONLARI
ON=100 999
DB.CONNECT
IF (<DB.CONNECTED>)
	DB.QUERY "SELECT category FROM craftitems WHERE id='<eval <DARGN>-100>'"
	IF (<DB.ROW.NUMROWS>)
		SRC.SYSMESSAGE <db.row.0.category>
		SRC.CTAG.Craft_Menu.Item_WHERE="category='<db.row.0.category>' AND <SRC.CTAG.Craft_Menu.Cat_WHERE>"
		SRC.CTAG.Craft_Menu.Item_INDEX=0
		SRC.CTAG.Craft_Menu.Item_ORDER="name ASC"

		DIALOGCLOSE D_CRAFT_MENU
		DIALOG D_CRAFT_MENU
	ENDIF		
ENDIF
DB.CLOSE

// KATEGORI PAGE ++
ON=1000
SRC.CTAG.Craft_Menu.Cat_INDEX +=10
DIALOGCLOSE D_CRAFT_MENU
DIALOG D_CRAFT_MENU

// KATEGORI PAGE --
ON=1001
IF (<SRC.CTAG.Craft_Menu.Cat_INDEX> > 0)
	SRC.CTAG.Craft_Menu.Cat_INDEX -=10
ENDIF
DIALOGCLOSE D_CRAFT_MENU
DIALOG D_CRAFT_MENU



// ITEM PAGE ++
ON=100000
SRC.CTAG.Craft_Menu.Item_INDEX +=9
DIALOGCLOSE D_CRAFT_MENU
DIALOG D_CRAFT_MENU

// ITEM PAGE --
ON=100001
IF (<SRC.CTAG.Craft_Menu.Item_INDEX> > 0)
	SRC.CTAG.Craft_Menu.Item_INDEX -=9
ENDIF
DIALOGCLOSE D_CRAFT_MENU
DIALOG D_CRAFT_MENU

ON=200000
SRC.MAKELAST
DIALOGCLOSE D_CRAFT_MENU
DIALOG D_CRAFT_MENU

ON=200001

DIALOGCLOSE D_CRAFT_MENU
DIALOG D_CRAFT_MENU



// ESYA URETME BUTONLARI
ON=10001 49999
DB.CONNECT
IF (<DB.CONNECTED>)
	DB.QUERY "SELECT baseid FROM craftitems WHERE id='<eval <DARGN>-10000>'"
	IF (<DB.ROW.NUMROWS>)
		LOCAL.ITEM <db.row.0.baseid>
	ENDIF		
ENDIF
DB.CLOSE

IF !(<SERV.ITEMDEF.<LOCAL.ITEM>>)
	SRC.CTAG.Craft_Menu.Notice <DEF.BFONT_RED>Bir hata oluştu. Bu eşya sunucuda kayıtlı değil.
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
	RETURN 1
	
ELSEIF !(<SRC.CANMAKESKILL <LOCAL.ITEM>>)
	SRC.CTAG.Craft_Menu.Notice <DEF.BFONT_RED>Gerekli beceri : <SERV.ITEMDEF.<LOCAL.ITEM>.SKILLMAKE>
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
	RETURN 1

ELSEIF !(<SRC.CANMAKE <LOCAL.ITEM>>)
	SRC.CTAG.Craft_Menu.Notice <DEF.BFONT_RED>Gerekli malzeme : <SERV.ITEMDEF.<LOCAL.ITEM>.RESMAKE>
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
	RETURN 1
ENDIF

IF (STRMATCH(*T_SCROLL*,<SERV.ITEMDEF.<LOCAL.ITEM>.TYPE>)) && (<LOCAL.ITEM> != I_SCROLL_BLANK)
	IF !(<SRC.ISCANCAST <LOCAL.ITEM>>)
		SRC.CTAG.Craft_Menu.Notice <DEF.BFONT_RED>Bu büyü kitabınızda yer almıyor veya büyüyü uygulayacak niteliğe sahip değilsin.
		DIALOGCLOSE D_CRAFT_MENU
		DIALOG D_CRAFT_MENU
		RETURN 1
	ENDIF
ENDIF
IF (<SRC.ISONLINE>)
	SRC.MAKEITEM <LOCAL.ITEM>
	SRC.SYSMESSAGE @044,,1,1  <SERV.ITEMDEF.<LOCAL.ITEM>.NAME> isimli eşyayı üretmeye başladın.
	SRC.CTAG.Craft_Menu.Notice <DEF.BFONT_GREEN><SERV.ITEMDEF.<LOCAL.ITEM>.NAME> isimli eşyayı üretmeye başladın.
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
ENDIF







// İtem İncele
ON=50001 69999
DB.CONNECT
IF (<DB.CONNECTED>)
	DB.QUERY "SELECT baseid FROM craftitems WHERE id='<eval <DARGN>-50000>'"
	IF (<DB.ROW.NUMROWS>)
		LOCAL.ITEM <db.row.0.baseid>
		SRC.CTAG0.CRAFT_MENU_ITEM_PROPS <LOCAL.ITEM>
		DIALOGCLOSE D_CRAFT_MENU_ITEM_PROPS 
		DIALOG D_CRAFT_MENU_ITEM_PROPS 
	ENDIF		
ENDIF
DB.CLOSE







[DIALOG D_CRAFT_MENU_ITEM_PROPS]
<DEF.DSP>
LOCAL.ITEM <SRC.CTAG0.CRAFT_MENU_ITEM_PROPS>
LOCAL.SKILL <SRC.CTAG0.Craft_Menu.Skill>
LOCAL.VALUE=<SERV.ITEMDEF.<LOCAL.ITEM>.VALUE>

PAGE 0

RESIZEPIC 0 0 5054 440 440
RESIZEPIC 10 10 5054 420 420
DHTMLGUMP 10 10 420 25 0 0 <DEF.BFONT_WHITE><DEF.CENTER><LOCAL.SKILL> / <SERV.ITEMDEF.<LOCAL.ITEM>.NAME> 
SRC.CTAG0.CRAFT_MENU_ITEM_PROPS <LOCAL.ITEM>
RESIZEPIC 30 50 0BB8 100 80 // TILEPIC
TILEPICHUE 60 70 <SERV.ITEMDEF.<LOCAL.ITEM>.ID> <DSERV.ITEMDEF.<LOCAL.ITEM>.RESDISPDNHUE>

RESIZEPIC 135 50 0BB8 285 80 // EŞYA BİLGİLERİ

DTEXT 145 60 0 Eşya : <StrTitle <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>>

DBUTTON 30,400,4015,4016,1,0,0,<DEF.SMSG_CUSTOM_COLOR>,Geri
DBUTTON 355,400,4006,4007,1,0,2,<DEF.SMSG_SUCCESS_COLOR>,Üret


RESIZEPIC 230 150 5054 190 240

IF !(STRMATCH('','<SERV.ITEMDEF.<LOCAL.ITEM>.SKILLMAKE>'))

	FOR 1 <ARRAYCOUNT <SERV.ITEMDEF.<LOCAL.ITEM>.SKILLMAKE.0.KEY>>
	LOCAL.SKILL_ID <SERV.ITEMDEF.<LOCAL.ITEM>.SKILLMAKE.<DLOCAL._FOR>.KEY>
	LOCAL.SKILL_VAL <SERV.ITEMDEF.<LOCAL.ITEM>.SKILLMAKE.<DLOCAL._FOR>.VAL>

	IF (STRMATCH('Skill_','<STRSUB 0 6 <LOCAL.SKILL_ID>>'))
	
		IF (<SRC.<SERV.SKILL.<LOCAL.SKILLMAKE_ID>.NAME>> >= <DLOCAL.SKILLMAKE_VAL>)
			dcroppedtext 240 <EVAL (<LOCAL._FOR>*35)+130> 135 25 <DEF.SMSG_CUSTOM_COLOR> <SRC.<SERV.SKILL.<LOCAL.SKILL_ID>.NAME>>/<FVAL <DLOCAL.SKILL_VAL>> <SERV.SKILL.<LOCAL.SKILL_ID>.NAME>
		ELSE
			dcroppedtext 240 <EVAL (<LOCAL._FOR>*35)+130> 135 25 0 <SRC.<SERV.SKILL.<LOCAL.SKILL_ID>.NAME>>/<FVAL <DLOCAL.SKILL_VAL>> <SERV.SKILL.<LOCAL.SKILL_ID>.NAME>
		ENDIF

	ELSEIF (STRMATCH('i_','<STRSUB 0 2 <LOCAL.SKILL_ID>>'))
		IF (<SRC.RESTEST 1 <LOCAL.SKILL_ID>>)
			dcroppedtext 240 <EVAL (<LOCAL._FOR>*35)+130> 135 25 <DEF.SMSG_CUSTOM_COLOR> <SERV.ITEMDEF.<LOCAL.SKILL_ID>.NAME>
		ELSE
			dcroppedtext 240 <EVAL (<LOCAL._FOR>*35)+130> 135 25 0 <SERV.ITEMDEF.<LOCAL.SKILL_ID>.NAME>
		ENDIF
	ENDIF

	ENDFOR
ENDIF

RESIZEPIC 30 150 5054 195 240

LOCAL.AUMENTAR=0
LOCAL.PAGE=1
PAGE 1 

IF !(STRMATCH('','<SERV.ITEMDEF.<LOCAL.ITEM>.RESOURCES>'))
	FOR 1 <ARRAYCOUNT <SERV.ITEMDEF.<LOCAL.ITEM>.RESOURCES.0.KEY>>
		IF (<LOCAL.AUMENTAR> > 5)
			LOCAL.AUMENTAR=0
			BUTTON 195 365 015A4 015A6 0 <EVAL <LOCAL.PAGE>+1> 0 
			LOCAL.PAGE += 1
			PAGE <EVAL <LOCAL.PAGE>>
			BUTTON 170 365 015A1 015A3 0 <EVAL <LOCAL.PAGE>-1> 0 
		ENDIF
		LOCAL.AUMENTAR += 1
		LOCAL.ID <SERV.ITEMDEF.<LOCAL.ITEM>.RESOURCES.<DLOCAL._FOR>.KEY>
		LOCAL.ITEMAMOUNT <SERV.ITEMDEF.<LOCAL.ITEM>.RESOURCES.<DLOCAL._FOR>.VAL>
		IF (<SRC.RESTEST <DLOCAL.ITEMAMOUNT> <LOCAL.ID>>)
			dcroppedtext 80 <EVAL (<LOCAL.AUMENTAR>*35)+130> 135 25 <DEF.SMSG_CUSTOM_COLOR> <SRC.PACKITEMCOUNTER <LOCAL.ID>>/<DLOCAL.ITEMAMOUNT> <SERV.ITEMDEF.<LOCAL.ID>.NAME>
		ELSE
			dcroppedtext 80 <EVAL (<LOCAL.AUMENTAR>*35)+130> 135 25 <DEF.SMSG_ERROR_COLOR> <SRC.PACKITEMCOUNTER <LOCAL.ID>>/<DLOCAL.ITEMAMOUNT> <SERV.ITEMDEF.<LOCAL.ID>.NAME>
		ENDIF
		TILEPICHUE 30 <EVAL (<LOCAL.AUMENTAR>*35)+130> <SERV.ITEMDEF.<LOCAL.ID>.ID> <dSERV.ITEMDEF.<LOCAL.ID>.RESDISPDNHUE>
	ENDFOR
ENDIF



[DIALOG D_CRAFT_MENU_ITEM_PROPS BUTTON]
ON=0
DIALOGCLOSE D_CRAFT_MENU
DIALOG D_CRAFT_MENU


ON=2
LOCAL.ITEM <SRC.CTAG0.CRAFT_MENU_ITEM_PROPS>
LOCAL.SKILL <SRC.CTAG0.Craft_Menu.Skill>
	
IF !(<serv.itemdef.<LOCAL.ITEM>>)
	SRC.CTAG.Craft_Menu.Notice= <DEF.BFONT_RED>Bir hata oluştu. Bu eşya sunucuda kayıtlı değil.
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
	RETURN 1
	
ELSEIF !(<SRC.CANMAKESKILL <LOCAL.ITEM>>)
	SRC.CTAG.Craft_Menu.Notice= <DEF.BFONT_RED>Gerekli beceri: <SERV.ITEMDEF.<LOCAL.ITEM>.SKILLMAKE>
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
	RETURN 1
	
ELSEIF !(<SRC.CANMAKE <LOCAL.ITEM>>)
	SRC.CTAG.Craft_Menu.Notice= <DEF.BFONT_RED>Gerekli malzeme: <SERV.ITEMDEF.<LOCAL.ITEM>.RESMAKE>
	DIALOGCLOSE D_CRAFT_MENU
	DIALOG D_CRAFT_MENU
	RETURN 1
ENDIF

IF (STRMATCH(*T_SCROLL*,<SERV.ITEMDEF.<LOCAL.ITEM>.TYPE>)) && (<LOCAL.ITEM> != I_SCROLL_BLANK)
	IF !(<SRC.ISCANCAST <LOCAL.ITEM>>)
		SRC.CTAG.Craft_Menu.Notice = <DEF.BFONT_RED>Bu büyü kitabınızda yer almıyor veya bu büyüyü uygulayacak özelliğe sahip değilsin.
		DIALOGCLOSE D_CRAFT_MENU
		DIALOG D_CRAFT_MENU
		RETURN 1
	ENDIF
ENDIF
SRC.MAKEITEM <LOCAL.ITEM>
SRC.SYSMESSAGE @044,,1,1  <SERV.ITEMDEF.<TAG0.CRAFT_LASTMAKE_BASEID>.NAME> isimli eşyayı üretmeye başladın.
SRC.CTAG.Craft_Menu.Notice= <StrTitle <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>> isimli eşyayı üretmeye başladın.
DIALOGCLOSE D_CRAFT_MENU
DIALOG D_CRAFT_MENU
		
		
[eof]