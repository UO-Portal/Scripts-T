

[ITEMDEF I_GMPAGE]
DEFNAME=I_GMPAGE
ID=I_MEMORY
TYPE=T_EQ_SCRIPT
VALUE=0
WEIGHT=0
LAYER=30
NAME=GMPage

ON=@CREATE
ATTR=ATTR_INVIS|ATTR_NEWBIE|ATTR_DECAY

ON=@EQUIP
IF (<SERV.LogMask>&000800)
    SERV.LOG Yeni GMPage (Name:<topobj.name>, UID:<topobj>, P: <TOPOBJ.P>, Lokasyon: <TOPOBJ.REGION.NAME>, IP: <TOPOBJ.ACCOUNT.LASTIP>)
ENDIF
SRC.SYSMESSAGE GMPage iletildi, <EVAL <SRC.GMPAGESEQ>> siradasiniz.




[FUNCTION GMPAGEWIPE]
FORINSTANCES I_GMPAGE
    REMOVE
ENDFOR
IF (<SERV.LogMask>&000800)
    SERV.LOG Tum GMPageler <NAME> tarafindan silindi.
ENDIF
SERV.B Tum GMPageler silinmistir.




[FUNCTION GMPAGESEQ]
FORINSTANCES I_GMPAGE
    LOCAL.SEQ ++
ENDFOR
RETURN <LOCAL.SEQ>



[FUNCTION GMPAGEADD]
IF !(<FINDID.I_GMPAGE>)
    SERV.NEWITEM I_GMPAGE
    NEW.EQUIP <UID>
    NEW.TAG0.CONTENT=<ARGS>
    NEW.TAG0.CREATED_AT=<SERV.RTIME>
    NEW.MOREP=<P>
    NEW.TAG0.TIME = <SERV.TIME>
ELSE
    SYSMESSAGE GMPage guncellendi.
    IF (<SERV.LogMask>&000800)
        SERV.LOG GMPage Guncellendi. (Name:<name>, UID:<uid>, P: <P>, Lokasyon: <REGION.NAME>, IP: <ACCOUNT.LASTIP>)
    ENDIF
    FINDID.I_GMPAGE.TAG0.CONTENT=<ARGS>
ENDIF


[FUNCTION GMPageList]
FORINSTANCES I_GMPAGE
    LOCAL.GMPAGES ++
    LOCAL.GMPAGE.<DLOCAL.GMPAGES> <UID>
ENDFOR



[PLEVEL 2]
PG

[FUNCTION PG]
CALL GMPageList
IF (<DLOCAL.GMPAGES>)
    DIALOGCLOSE d_gmpage_list
    DIALOG d_gmpage_list
ELSE
    SYSMESSAGE @38,,1,1 GMPage yok.
ENDIF

[DIALOG d_gmpage_list]
<DEF.DlgStartPos>

CALL GMPageList

PAGE 0
RESIZEPIC 0 0 9200 520 440
RESIZEPIC 10 10 3000 500 420

DHTMLGUMP 10 10 500 25 1 0 <DEF.CENTER><SERV.NAME> GMPage (<DLOCAL.GMPAGES>)<DEF.CENTERE>

PAGE 1

LOCAL.Max=<DLOCAL.GMPAGES>
IF (<DLOCAL.Max> > 30)	// Fazla yukseltmeyin client cokmelerine sebep olabilir
    LOCAL.Max=30
ENDIF

IF (<DLOCAL.Max>)
    LOCAL.Page=1
        FOR 0 <eval <DLOCAL.Max>-1>
            IF (<DLOCAL._FOR> > 0) && !(<eval <DLOCAL._FOR>%5>)
                LOCAL.Page ++
                BUTTON 470 400 5540 5541 0 <DLOCAL.Page> 0
                PAGE <LOCAL.Page>
                BUTTON 440 400 5537 5538 0 <eval <DLOCAL.Page>-1> 0
            ENDIF
            REF1=<LOCAL.GMPAGE.<DLOCAL.GMPAGES>>
            IF (<REF1>)
                REF2=<REF1.TOPOBJ>
                BUTTON 20 <eval 50+((<LOCAL._FOR>%5)*65)> 5540 5541 1 0 <eval <LOCAL._FOR>+1>
                DHTMLGUMP 40 <eval 50+((<LOCAL._FOR>%5)*65)> 450 60 1 1 <DEF.U><REF2.NAME><DEF.UE> <QVAL (<REF2.ISONLINE>)? <DEF.BFONT_DGREEN>(Çevrimiçi):<DEF.BFONT_DRED>(Çevrimdışı)><DEF.BFONT_BLACK>: <DEF.BR><REF1.TAG0.CONTENT>
            ENDIF
        ENDFOR
ELSE
    DHTMLGUMP 20 40 480 380 1 0 <DEF.BR><DEF.BR><DEF.BR><DEF.BR><DEF.BR><DEF.BR><DEF.BR><DEF.CENTER><DEF.BFONT_DRED>GMPage yok.
ENDIF


[DIALOG d_gmpage_list BUTTON]
ON=1 50
CALL GMPageList
REF1=<LOCAL.GMPAGE.<DARGN>>
IF (<REF1>)
    SRC.CTAG.Dialog.GMPage.Selected=<REF1>
    REF1.DIALOG d_gmpage_info
ENDIF



[DIALOG d_gmpage_info]
<DEF.DlgStartPos>

RESIZEPIC 0 0 9200 520 440
RESIZEPIC 10 10 3000 500 420

DHTMLGUMP 10 10 500 25 1 0 <DEF.CENTER><SERV.NAME> GMPage Info<DEF.CENTERE>

DORIGIN 20 40

DTEXT +0 +0 0 Gönderim Zamanı:
DTEXT +140 +0 0 <FormatMinutes <EVAL <TAG0.TIME>/<SERV.TIME>>> önce

BUTTON +0 +30 4006 4007 1 0 1
DTEXT +30 +30 0 Oyuncu:
DTEXT +140 +30 0 <TOPOBJ.NAME> (<QVAL (<TOPOBJ.ISONLINE>)? Çevrimiçi:Çevrimdışı>)

BUTTON +0 +50 4006 4007 1 0 2
DTEXT +30 +50 0 Lokasyon:
DTEXT +140 +50 0 <MOREP> (<TOPOBJ.REGION.NAME>)

BUTTON +0 +70 4006 4007 1 0 3
dtext +30 +70 0 İlgilenen:
dtext +140 +70 0 <QVAL (<LINK>)? <LINK.NAME>:Yok>

DTEXT +140 +90 0 İçerik:
DHTMLGUMP +140 +110 335 100 1 1 <TAG0.CONTENT>

DTEXT +140 +210 0 Cevap:
RESIZEPIC +140 +230 3000 335 100
DTEXTENTRY +145 +235 320 90 0 0 


// Gonder
BUTTON +140 +340 2076 2075 1 0 5

DORIGIN 20 200

// Sil 
BUTTON +0 +0 4020 4022 1 0 4
DTEXT +30 +0 0 Sil


[DIALOG d_gmpage_info BUTTON]
ON=0
SRC.PG

// Oyuncuya git
ON=1
SRC.GO <TOPOBJ.P>
DIALOGCLOSE d_gmpage_info
DIALOG d_gmpage_info


// Lokasyona git
ON=2
SRC.GO <MOREP>
DIALOGCLOSE d_gmpage_info
DIALOG d_gmpage_info


// İlgilen
ON=3
REF1=<LINK>
IF (<REF1>) && (<REF1.ISGM>)
    SRC.SYSMESSAGE @38,,1,1 Bu page ile zaten <REF1.NAME> ilgileniyor.
    RETURN 1
ENDIF
LINK=<SRC>
IF (<SERV.LogMask>&000800)
    SERV.LOG [<src.name>/<uid>] GMPage ile ilgileniyor. (Name:<topobj.name>, UID:<topobj>, P: <TOPOBJ.P>, Lokasyon: <TOPOBJ.REGION.NAME>, IP: <TOPOBJ.ACCOUNT.LASTIP>)
ENDIF
SRC.SYSMESSAGE Bu page ile artik siz ilgileniyorsunuz.
DIALOGCLOSE d_gmpage_info
DIALOG d_gmpage_info


// Sil
ON=4
IF (<ISITEM>) 
    IF (<SERV.LogMask>&000800)
        SERV.LOG [<src.name>/<uid>] GMPage silindi. (Name:<topobj.name>, UID:<topobj>, P: <TOPOBJ.P>, Lokasyon: <TOPOBJ.REGION.NAME>, IP: <TOPOBJ.ACCOUNT.LASTIP>)
    ENDIF
    SRC.SYSMESSAGE @38,,1,1 GMPage silindi.
    REMOVE
ENDIF
SRC.PG


// Cevapla
ON=5
IF (<TOPOBJ.ISONLINE>)
    TAG0.RESPONSE=<ARGTXT[0]>
    TRYSRC <TOPOBJ.UID> DIALOGCLOSE d_gmpage_response
    TRYSRC <TOPOBJ.UID> DIALOG d_gmpage_response
ENDIF
IF (<SERV.LogMask>&000800)
    SERV.LOG [<src.name>/<uid>] GMPage cevaplandi. (Name:<topobj.name>, UID:<topobj>, P: <TOPOBJ.P>, Lokasyon: <TOPOBJ.REGION.NAME>, IP: <TOPOBJ.ACCOUNT.LASTIP>) Cevap: <ARGTXT[0]>
ENDIF
SRC.PG



[DIALOG d_gmpage_response]
<DEF.DlgStartPos>

PAGE 1
DORIGIN 20 40

BUTTON +0 +2 2117 2118 0 2 0
DTEXT +20 +0 43 Mesaj 

page 2
RESIZEPIC 0 0 9200 540 340
RESIZEPIC 10 10 3000 520 320

DHTMLGUMP 10 10 520 25 1 0 <DEF.CENTER><SERV.NAME> GMPage Cevap<DEF.CENTERE>

DORIGIN 20 40

DTEXT +0 +0 0 Giden;
DHTMLGUMP +0 +20 500 100 1 1 <TAG0.CONTENT>

DTEXT +0 +120 0 Cevap;
DHTMLGUMP +0 +140 500 100 1 1 <TAG0.RESPONSE>

BUTTON +0 +250 2076 2075 1 0 1

[DIALOG d_gmpage_response BUTTON]
ON=1
IF (<SERV.LogMask>&000800)
    SERV.LOG GMPage okudu. (Name:<topobj.name>, UID:<topobj>, P: <TOPOBJ.P>, Lokasyon: <TOPOBJ.REGION.NAME>, IP: <TOPOBJ.ACCOUNT.LASTIP>)
ENDIF
IF (<ISITEM>)
    REMOVE
ENDIF

[eof]