
[COMMENT sphere_skillmenu]
The sphereserver has a number of hardcoded skillmenu's, this file contains the softcoded component.  The prupose of each is as follows:

// Craft Menu
sm_alchemy			= Triggered by DClicking a alchemy tool
sm_cartography		= Triggered by DClicking a cartography tool
sm_bowcraft			= Triggered by DClicking a knife and using it on shaft
sm_blacksmith			= Triggered by DClicking a blacksmithing hammer
sm_carpentry			= Triggered by DClicking a carpentry tool
sm_tailor		= Triggered by DClicking a sewing kit 
sm_tinker				= Triggered by DClicking tinker tools
sm_inscription		= Triggered by DClicking a blank scroll

[DEFNAME CRAFT_MENU_DEFS]
// ALCHEMY
CRAFT_SKILL_ALCHEMY_ITEMLIST I_POTION_HEALLESS,I_POTION_HEAL,I_POTION_HEALGREAT,I_POTION_CURELESS,I_POTION_CURE,I_POTION_CUREGREAT,I_POTION_REFRESH,I_POTION_REFRESHTOTAL,I_POTION_STRENGTH,I_POTION_STRENGTHGREAT,I_POTION_AGILITY,I_POTION_AGILITYGREAT,I_POTION_EXPLOSIONLESS,I_POTION_EXPLOSION,I_POTION_EXPLOSIONGREAT,I_POTION_POISONLESS,I_POTION_POISON,I_POTION_POISONGREAT,I_POTION_POISONDEADLY,I_POTION_MANA,I_POTION_MANATOTAL,I_POTION_NIGHTSIGHT

// BOWCRAFT
CRAFT_SKILL_BOWCRAFT_ITEMLIST I_KINDLING,I_ARROW_SHAFT,I_ARROW,I_XBOLT,I_BOW,I_CROSSBOW,I_CROSSBOW_HEAVY

// CARPENTRY
CRAFT_SKILL_CARPENTRY_ITEMLIST I_CHAIR_THRONE,I_CHAIR_WOOD_ROUGH_DARK,I_CHAIR_WOOD_FANCY_CUSHION,I_CHAIR_WOOD_CUSHION,I_CHAIR_WOOD_2,I_CHAIR_STOOL_BAR,I_CHAIR_STOOL,I_BENCH_WOOD_DARK, I_ARMOIR_DK,I_ARMOIR_LT,I_ARMOIRE_CHERRY,I_ARMOIRE_MAPLE,I_CABINET_SHORT,I_CABINET_TALL,I_WOODEN_CHEST_FINISHED,I_WOODEN_FOOT_LOCKER,I_WOODEN_CHEST_GILDED,I_WOODEN_CHEST_ORNATE,I_WOODEN_CHEST_PLAIN,I_ARMOIRE_ELEGANT,I_ARMOIRE_RED,I_CHEST_WOODEN_BRASS,I_BOX_WOOD,I_CRATE_SMALL,I_CRATE_MD,I_CRATE_LG, I_TABLE_LOW1,I_TABLE_LOW2,I_TABLE_NIGHTSTAND,I_TABLE_WOOD_ROUGH_SMALL,I_TABLE_WOOD_ROUGH_BOARD,I_TABLE_WRITING, I_CLUB,I_STAFF_GNARLED,I_STAFF_QUARTER,I_SHIELD_WOOD,I_SHEPHERDS_CROOK,I_FISHING_POLE, I_BAR_DOOR_NE,I_BAR_PIECE,I_BOOKCASE,I_BOOKCASE_FULL,I_BUREAU_2,I_MUSIC_STAND_TALL,I_DRUM,I_LYRE,I_LUTE,I_HARP_LARGE,I_TAMBOURINE,I_TAMBOURINE_TAS,I_SCROLL_BLANK,I_TROPHY_MOUNTBOARD_LG,I_MODEL_SHIP,I_BARREL_LID,I_DRESS_FORM,I_EASEL,I_BULLETIN_BOARD,I_DARTBOARD,I_BARREL_OPEN,I_MUSIC_STAND,I_EASEL_WITH_CANVAS,I_SADDLE

// CARTOGRAPHY
CRAFT_SKILL_CARTOGRAPHY_ITEMLIST I_MAP_LOCAL,I_MAP_CITY,I_MAP_SEA_CHART,I_MAP_WORLD

// INSCRIPTION
CRAFT_SKILL_INSCRIPTION_ITEMLIST I_SCROLL_CLUMSY,I_SCROLL_CREATE_FOOD,I_SCROLL_FEEBLEMIND,I_SCROLL_HEAL,I_SCROLL_MAGIC_ARROW,I_SCROLL_NIGHT_SIGHT,I_SCROLL_REACTIVE_ARMOR,I_SCROLL_WEAKEN, I_SCROLL_AGILITY,I_SCROLL_CUNNING,I_SCROLL_CURE,I_SCROLL_HARM,I_SCROLL_PROTECTION,I_SCROLL_STRENGTH, I_SCROLL_BLESS,I_SCROLL_FIREBALL,I_SCROLL_POISON,I_SCROLL_TELEPORT,I_SCROLL_WALL_OF_STONE, I_SCROLL_ARCHCURE,I_SCROLL_ARCHPROTECTION,I_SCROLL_CURSE,I_SCROLL_FIRE_FIELD,I_SCROLL_GREATER_HEAL,I_SCROLL_LIGHTNING,I_SCROLL_MANA_DRAIN,I_SCROLL_RECALL, I_SCROLL_BLADE_SPIRITS,I_SCROLL_DISPEL_FIELD,,I_SCROLL_MAGIC_REFLECTION,I_SCROLL_MIND_BLAST,I_SCROLL_PARALYZE,I_SCROLL_POISON_FIELD,I_SCROLL_SUMMON_CREATURE, I_SCROLL_DISPEL,I_SCROLL_ENERGY_BOLT,I_SCROLL_EXPLOSION,I_SCROLL_INVISIBILITY,I_SCROLL_MARK,I_SCROLL_MASS_CURSE,I_SCROLL_PARALYZE_FIELD,I_SCROLL_REVEAL, I_SCROLL_CHAIN_LIGHTNING,I_SCROLL_ENERGY_FIELD,I_SCROLL_FLAMESTRIKE,I_SCROLL_GATE_TRAVEL,I_SCROLL_MANA_VAMPIRE,I_SCROLL_MASS_DISPEL,I_SCROLL_METEOR_SWARM, I_SCROLL_EARTHQUAKE,I_SCROLL_ENERGY_VORTEX,I_SCROLL_RESURRECTION,I_SCROLL_SUMMON_ELEM_AIR,I_SCROLL_SUMMON_DAEMON,I_SCROLL_SUMMON_ELEM_EARTH,I_SCROLL_SUMMON_ELEM_FIRE,I_SCROLL_SUMMON_ELEM_WATER

// TAILOR
CRAFT_SKILL_Tailoring_ITEMLIST I_BOOTS_CALF,I_BOOTS_THIGH,I_SHOES_PLAIN,I_SANDALS,I_BANDANA,I_BONNET,I_CAP,I_HAT_WIZARDS,I_HAT_FEATHER,I_HAT_FLOPPY,I_HAT_WIDE_BRIM,I_HAT_JESTER,I_HAT_TRICORNE,I_SKULL_CAP,I_HAT_STRAW,I_HAT_STRAW_TALl,I_KILT,I_PANTS_LONG,I_PANTS_SHORT,I_SKIRT_LONG, I_CAPE,I_DOUBLET,I_DRESS_FULL,I_SHIRT_FANCY,I_JESTER_SUIT,I_DRESS_PLAIN,I_ROBE,I_SHIRT_PLAIN,I_SURCOAT,I_TUNIC, I_LEATHER_GLOVES,I_LEATHER_GORGET,I_LEATHER_LEGGINGS,I_LEATHER_SLEEVES,I_LEATHER_TUNIC,I_LEATHER_CAP, I_STUDDED_GLOVES,I_STUDDED_GORGET,I_STUDDED_LEGGINGS,I_STUDDED_SLEEVES,I_STUDDED_TUNIC, I_CURTAIN_SASH,I_CURTAIN_FREESTANDING,I_CURTAIN_BANNER_NW,I_MASK_BEAR,I_MASK_DEER,I_MASK_BLUE,I_MASK_TRIBAL_RED,I_BASKET_SQUARE,I_BACKPACK,I_BAG,I_SASH,I_APRON_FULL,I_APRON_HALF

// TINKERING
CRAFT_SKILL_TINKERING_ITEMLIST I_BRACELET_GOLD,I_EARRINGS_GOLD,I_NECKLACE_GOLD_BEADS,I_NECKLACE_GOLD,I_NECKLACE_METAL_BEADS,I_RING_GOLD,I_NAILS,I_HINGE,I_BARREL_TAP,I_CLOCK_PARTS,I_GEARS,I_SEXTANT_PARTS,I_SPRINGS,I_AXEL_AND_GEARS,I_AXEL,I_SAW,I_HATCHET,I_SEWING_KIT,I_SCISSORS,I_MORTAR_PESTLE,I_LOCKPICK,I_HAMMER_SMITH,I_PICKAXE,I_SHOVEL,I_GOBLET,I_KNIFE_SKINNING,I_PLATE,I_CLEAVER_MEAT,I_MUG_PEWTER,I_SPOON,I_FORK,I_KNIFE,I_KEY_COPPER,I_SEXTANT,I_POUCH_TRAPPED,I_CANDLEABRA,I_HEATING_STAND,I_LANTERN,I_CLOCK,I_GLOBE,I_CHEST_METAL,I_SCALES,I_CANDLE

CRAFT_SKILL_BLACKSMITH_ITEMLIST I_PLATEMAIL_CHEST,I_PLATEMAIL_GLOVES,I_PLATEMAIL_GORGET,I_PLATEMAIL_LEGGINGS,I_PLATEMAIL_ARMS, I_SHIELD_ROUND_BRONZE,I_SHIELD_BUCKLER,I_SHIELD_KITE_METAL,I_SHIELD_HEATER,I_SHIELD_ROUND_METAL, I_PLATEMAIL_HELM,I_HELM_CLOSED,I_HELM_OPEN,I_BASCINET,I_HELM_NOSE, I_AXE,I_AXE_BATTLE,I_AXE_DOUBLE,I_AXE_TWO_HAND,I_AXE_BATTLE_LARGE,I_AXE_WAR,I_MACE,I_MAUL,I_HAMMER_WAR,I_MACE_WAR,I_KNIFE_BUTCHER,I_DAGGER,I_KNIFE_SKINNING,I_BARDICHE,I_HALBERD,I_PITCHFORK,I_SPEAR_SHORT,I_SPEAR,I_WAR_FORK,I_CUTLASS,I_SWORD_BROAD,I_KATANA,I_KRYSS,I_SWORD_LONG,I_SCIMITAR,I_SWORD_VIKING, I_SHADOW_PLATEMAIL_CHEST,I_SHADOW_PLATEMAIL_ARMS,I_SHADOW_PLATEMAIL_LEGGINGS,I_SHADOW_PLATEMAIL_GORGET,I_SHADOW_PLATEMAIL_GAUNTLETS,I_SHADOW_BUCKLER_SHIELD,I_SHADOW_HEATER_SHIELD,I_SHADOW_METAL_SHIELD,I_SHADOW_PLATE_HELM,I_SHADOW_CLOSED_HELM,I_SHADOW_HELMET,I_SHADOW_BASCINET,I_SHADOW_NOSE_HELM, I_AGAPITE_PLATEMAIL_CHEST,I_AGAPITE_PLATEMAIL_ARMS,I_AGAPITE_PLATEMAIL_LEGGINGS,I_AGAPITE_PLATEMAIL_GORGET,I_AGAPITE_PLATEMAIL_GAUNTLETS,I_AGAPITE_BUCKLER_SHIELD,I_AGAPITE_HEATER_SHIELD,I_AGAPITE_METAL_SHIELD,I_AGAPITE_PLATE_HELM,I_AGAPITE_CLOSED_HELM,I_AGAPITE_HELMET,I_AGAPITE_BASCINET,I_AGAPITE_NOSE_HELM, I_MYTHERIL_PLATEMAIL_CHEST,I_MYTHERIL_PLATEMAIL_ARMS,I_MYTHERIL_PLATEMAIL_LEGGINGS,I_MYTHERIL_PLATEMAIL_GORGET,I_MYTHERIL_PLATEMAIL_GAUNTLETS, I_MYTHERIL_BUCKLER_SHIELD,I_MYTHERIL_HEATER_SHIELD,I_MYTHERIL_METAL_SHIELD,I_MYTHERIL_PLATE_HELM,I_MYTHERIL_CLOSED_HELM,I_MYTHERIL_HELMET,I_MYTHERIL_BASCINET,I_MYTHERIL_NOSE_HELM, I_VERITE_PLATEMAIL_CHEST,I_VERITE_PLATEMAIL_ARMS,I_VERITE_PLATEMAIL_LEGGINGS,I_VERITE_PLATEMAIL_GORGET,I_VERITE_PLATEMAIL_GAUNTLETS,I_VERITE_BUCKLER_SHIELD,I_VERITE_HEATER_SHIELD,I_VERITE_METAL_SHIELD,I_VERITE_PLATE_HELM,I_VERITE_CLOSED_HELM,I_VERITE_HELMET,I_VERITE_BASCINET,I_VERITE_NOSE_HELM, I_VALORITE_PLATEMAIL_CHEST,I_VALORITE_PLATEMAIL_ARMS,I_VALORITE_PLATEMAIL_LEGGINGS,I_VALORITE_PLATEMAIL_GORGET,I_VALORITE_PLATEMAIL_GAUNTLETS,I_VALORITE_BUCKLER_SHIELD,I_VALORITE_HEATER_SHIELD,I_VALORITE_METAL_SHIELD,I_VALORITE_PLATE_HELM,I_VALORITE_CLOSED_HELM,I_VALORITE_HELMET,I_VALORITE_BASCINET,I_VALORITE_NOSE_HELM, I_BLACKROCK_PLATEMAIL_CHEST,I_BLACKROCK_PLATEMAIL_ARMS,I_BLACKROCK_PLATEMAIL_LEGGINGS,I_BLACKROCK_PLATEMAIL_GORGET,I_BLACKROCK_PLATEMAIL_GAUNTLETS,I_BLACKROCK_BUCKLER_SHIELD,I_BLACKROCK_HEATER_SHIELD,I_BLACKROCK_METAL_SHIELD,I_BLACKROCK_PLATE_HELM,I_BLACKROCK_CLOSED_HELM,I_BLACKROCK_HELMET,I_BLACKROCK_BASCINET,I_BLACKROCK_NOSE_HELM, I_BLOODROCK_PLATEMAIL_CHEST,I_BLOODROCK_PLATEMAIL_ARMS,I_BLOODROCK_PLATEMAIL_LEGGINGS,I_BLOODROCK_PLATEMAIL_GORGET,I_BLOODROCK_PLATEMAIL_GAUNTLETS,I_BLOODROCK_BUCKLER_SHIELD,I_BLOODROCK_HEATER_SHIELD,I_BLOODROCK_METAL_SHIELD,I_BLOODROCK_PLATE_HELM,I_BLOODROCK_CLOSED_HELM,I_BLOODROCK_HELMET,I_BLOODROCK_BASCINET,I_BLOODROCK_NOSE_HELM


[FUNCTION Func_Craft_Menu_Rebuild_All]	
Func_Craft_Menu_Rebuild_Single Alchemy
Func_Craft_Menu_Rebuild_Single Bowcraft
Func_Craft_Menu_Rebuild_Single Carpentry
Func_Craft_Menu_Rebuild_Single Cartography
Func_Craft_Menu_Rebuild_Single Inscription
Func_Craft_Menu_Rebuild_Single Tailoring
Func_Craft_Menu_Rebuild_Single Blacksmith
Func_Craft_Menu_Rebuild_Single Tinkering



[FUNCTION Func_Craft_Menu_Rebuild_Single]	
LDB.CONNECT "db/craftdialog.db"
IF (<LDB.CONNECTED>)	
	LOCAL.SKILLNAME <STRTOUPPER <ARGS>>
	
	ldb.EXECUTE DROP TABLE IF EXISTS '<LOCAL.SKILLNAME>'
	LDB.EXECUTE CREATE TABLE IF NOT EXISTS '<LOCAL.SKILLNAME>' ('id' INTEGER PRIMARY KEY AUTOINCREMENT NOT NULL, 'baseid' TEXT, 'name' TEXT, 'type' TEXT, 'category' TEXT, 'subsection' TEXT, 'description' TEXT, 'skill_key' TEXT, 'skill_val' TEXT, 'created_at' TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP, 'updated_at' TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP)

	LDB.EXECUTE BEGIN
				
	// 
	FOR 1 <ARRAYCOUNT <DEF.CRAFT_SKILL_<LOCAL.SKILLNAME>_ITEMLIST>>
		LOCAL.ITEM=<ARRAY <DEF.CRAFT_SKILL_<LOCAL.SKILLNAME>_ITEMLIST>,<DLOCAL._FOR>>
		
		IF (<SERV.ITEMDEF.<LOCAL.ITEM>>)
			LOCAL.SKILL_KEY=<SERV.ITEMDEF.<LOCAL.ITEM>.SKILLMAKE.1.KEY>
			LOCAL.SKILL_VAL=<SERV.ITEMDEF.<LOCAL.ITEM>.SKILLMAKE.1.VAL>
			
			LOCAL.CATEGORY=<QVAL (<ISEMPTY <SERV.ITEMDEF.<LOCAL.ITEM>.CATEGORY>>)?Uncategorized:<SERV.ITEMDEF.<LOCAL.ITEM>.CATEGORY>>
			LOCAL.SUBSECTION=<QVAL (<ISEMPTY <SERV.ITEMDEF.<LOCAL.ITEM>.SUBSECTION>>)?Unsubsection:<SERV.ITEMDEF.<LOCAL.ITEM>.SUBSECTION>>
			LOCAL.DESCRIPTION=<QVAL (<ISEMPTY <SERV.ITEMDEF.<LOCAL.ITEM>.DESCRIPTION>>)?Undescription:<SERV.ITEMDEF.<LOCAL.ITEM>.DESCRIPTION>>
			LDB.QUERY "SELECT id FROM '<LOCAL.SKILLNAME>' WHERE baseid='<STRTOUPPER <LOCAL.ITEM>>'"
			
			IF (<DLDB.ROW.NUMROWS>)
				lDB.EXECUTE "UPDATE '<LOCAL.SKILLNAME>' SET name='<SERV.ITEMDEF.<LOCAL.ITEM>.NAME>', type='<STRTOUPPER <SERV.ITEMDEF.<LOCAL.ITEM>.TYPE>>', category='<LOCAL.CATEGORY>', subsection='<LOCAL.subsection>', description='<LOCAL.description>', skill_key='<STRTOUPPER <LOCAL.SKILL_KEY>>', skill_val='<FVAL <DLOCAL.SKILL_VAL>>' WHERE baseid='<STRTOUPPER <LOCAL.ITEM>>'"
			ELSE
				LDB.EXECUTE INSERT INTO '<LOCAL.SKILLNAME>' ('baseid','name','type','category','subsection','description','skill_key','skill_val') VALUES ("<STRTOUPPER <LOCAL.ITEM>>","<SERV.ITEMDEF.<LOCAL.ITEM>.NAME>","<STRTOUPPER <SERV.ITEMDEF.<LOCAL.ITEM>.TYPE>>","<LOCAL.CATEGORY>","<LOCAL.SUBSECTION>","<LOCAL.DESCRIPTION>","<STRTOUPPER <LOCAL.SKILL_KEY>>","<FVAL <DLOCAL.SKILL_VAL>>")
			ENDIF
			
		ENDIF
	ENDFOR

	LDB.EXECUTE COMMIT

LDB.CLOSE
ENDIF




[FUNCTION CraftSkill]
CTAG.Craft_Menu.Skill=<STRTOUPPER <argv1>>

CTAG.Craft_Menu.WHERE="id"
CTAG.Craft_Menu.INDEX=0
CTAG.Craft_Menu.ORDER="name ASC"

CTAG.Craft_Menu.Cat_WHERE="id"
CTAG.Craft_Menu.Cat_INDEX=0

CTAG.Craft_Menu.Sub_WHERE="id"
CTAG.Craft_Menu.Sub_INDEX=0

CTAG.Craft_Menu.Notice=

DIALOGCLOSE d_craft_menu
DIALOG d_craft_menu


[FUNCTION TOOLS]
SERV.NEWITEM I_MORTAR_PESTLE,1,<FINDLAYER.21>,1
SERV.NEWITEM I_ARROW_SHAFT,1,<FINDLAYER.21>,1
SERV.NEWITEM I_SAW,1,<FINDLAYER.21>,1
// SERV.NEWITEM I_MAPMAKERS_PEN,100,<FINDLAYER.21>,1
SERV.NEWITEM I_SCROLL_BLANK,100,<FINDLAYER.21>,1
SERV.NEWITEM I_SEWING_KIT,1,<FINDLAYER.21>,1
SERV.NEWITEM I_TINKER_TOOLS,1,<FINDLAYER.21>,1
SERV.NEWITEM I_BOTTLE_EMPTY,100,<FINDLAYER.21>,1



[DIALOG d_craft_menu]
0,0

RESIZEPIC 0 0 5054 680 460
RESIZEPIC 10 10 3000 660 440

DHTMLGUMP 10 10 660 25 1 0 <def.center><serv.name> <CTAG.Craft_Menu.Skill> Menu

LDB.CONNECT "db/craftdialog.db"
IF (<LDB.CONNECTED>)

	DORIGIN 20 340
	
	DHTMLGUMP +0 +0 170 25 1 0 Son üretilen: <QVAL (<TAG0.CRAFT_LASTMAKE_BASEID>)?<DEF.BFONT_TURQ><SERV.ITEMDEF.<TAG0.CRAFT_LASTMAKE_BASEID>.NAME>:<DEF.BFONT_RED>Yok>
	// DHTMLGUMP +0 +30 170 25 1 0 <SERV.SKILL.<STRSUB 0 13 <SRC.CTAG.Craft_Menu.Skill>>.KEY> : <SRC.<SERV.SKILL.<STRSUB 0 13 <SRC.CTAG.Craft_Menu.Skill>>.KEY>>
	
	BUTTON +0 +70 4006 4007 1 0 1 
	DTEXT +30 +70 0 Tekraryap
	// Not
	
	IF !(<ISEMPTY <SRC.CTAG.Craft_Menu.Notice>>)
		DHTMLGUMP +180 +0 170 90 1 1 <DEF.CENTER><SRC.CTAG.Craft_Menu.Notice>
	ELSE
		DHTMLGUMP +180 +0 170 90 1 1 Not:
	ENDIF

	// Kategori
	DORIGIN 20 40
	RESIZEPIC +0 +20 5054 170 275
	lDB.QUERY "SELECT id, category FROM '<CTAG.Craft_Menu.Skill>' WHERE <SRC.CTAG.Craft_Menu.Cat_WHERE> GROUP BY category LIMIT <SRC.dCTAG.Craft_Menu.Cat_INDEX>,9"
	IF (<lDB.ROW.NUMROWS>)
		FOR cats 0 <EVAL <LDB.ROW.NUMROWS>-1>
			IF (<lDB.ROW.<DLOCAL.cats>.id>)
				LOCAL.CATCOUNT=<LDB.ROW.NUMROWS>
				IF (<ISEMPTY <LDB.ROW.<DLOCAL.cats>.category>>)
					DHTMLGUMP +10 <EVAL (<LOCAL.cats>*25)+70> 140 25 1 0 <def.bfont_white>Tümü
				ELSE
					DHTMLGUMP +10 <EVAL (<LOCAL.cats>*25)+70> 140 25 1 0 <def.bfont_white><LDB.ROW.<DLOCAL.cats>.category>
				ENDIF
				BUTTON +150 <EVAL (<LOCAL.cats>*25)+72> 015a4 015a5 1 0 <EVAL <LDB.ROW.<DLOCAL.cats>.id>+100>
			ENDIF			
		ENDFOR
	ENDIF

	DTEXT +10 +270 0 Sayfa: <EVAL (<SRC.CTAG.Craft_Menu.Cat_INDEX>/9)+1>
	IF (<EVAL (<dLOCAL.CATCOUNT>/9)>)
		BUTTON +150 +270 5601 5605 1 0 2 // ++ 
	ENDIF
	BUTTON +130 +270 5603 5607 1 0 3 // -- 
	DTEXT +0 +0 0 Bölüm (<dLOCAL.CATCOUNT>) ;
	
	
	// Alt Kategori
	DORIGIN 200 40
	RESIZEPIC +0 +20 5054 170 275
	lDB.QUERY "SELECT id, subsection FROM '<CTAG.Craft_Menu.Skill>' WHERE <SRC.CTAG.Craft_Menu.Sub_WHERE> GROUP BY subsection LIMIT <SRC.dCTAG.Craft_Menu.Sub_INDEX>,9"
	IF (<lDB.ROW.NUMROWS>)
		FOR subs 0 <EVAL <LDB.ROW.NUMROWS>-1>
			IF (<lDB.ROW.<DLOCAL.subs>.id>)
				LOCAL.SUBCOUNT=<LDB.ROW.NUMROWS>
				IF (<ISEMPTY <LDB.ROW.<DLOCAL.subs>.subsection>>)
					DHTMLGUMP +10 <EVAL (<LOCAL.subs>*25)+70> 140 25 1 0 <def.bfont_white>Tümü
				ELSE
					DHTMLGUMP +10 <EVAL (<LOCAL.subs>*25)+70> 140 25 1 0 <def.bfont_white><LDB.ROW.<DLOCAL.subs>.subsection>
				ENDIF
				BUTTON +150 <EVAL (<LOCAL.subs>*25)+72> 015a4 015a5 1 0 <EVAL <LDB.ROW.<DLOCAL.subs>.id>+200>
			ENDIF			
		ENDFOR
	ENDIF
	DTEXT +10 +270 0 Sayfa: <EVAL (<SRC.CTAG.Craft_Menu.Sub_INDEX>/9)+1>
	IF (<EVAL (<dLOCAL.SUBCOUNT>/9)>)
		BUTTON +150 +270 5601 5605 1 0 4 // ++ 
	ENDIF
	BUTTON +130 +270 5603 5607 1 0 5 // -- 
	DTEXT +0 +0 0 Alt Bölüm (<dLOCAL.SUBCOUNT>) ;

	
	// Items
	DORIGIN 380 40
	RESIZEPIC +0 +20 5054 270 370
	lDB.QUERY "SELECT * FROM '<CTAG.Craft_Menu.Skill>' WHERE <SRC.CTAG.Craft_Menu.WHERE> ORDER BY <SRC.CTAG.Craft_Menu.ORDER> LIMIT <SRC.dCTAG.Craft_Menu.INDEX>,9"
	IF (<lDB.ROW.NUMROWS>)
	LOCAL.ITEMCOUNT=<LDB.ROW.NUMROWS>	
		FOR items 0 <EVAL <LDB.ROW.NUMROWS>-1>
			IF (<lDB.ROW.<DLOCAL.items>.id>)
				LOCAL.ITEM <LDB.ROW.<DLOCAL.items>.baseid>
				IF !(<CANMAKESKILL <LOCAL.ITEM>>)
					LOCAL.TEXTHUE = 38
				ELSEIF !(<CANMAKE <LOCAL.ITEM>>)
					LOCAL.TEXTHUE = 38
				ELSE
					LOCAL.TEXTHUE = 044
				ENDIF
				BUTTON +10 <EVAL (<DLOCAL.items>*35)+80> 210 211 1 0 <EVAL <lDB.ROW.<DLOCAL.items>.id>+1000>
				DCROPPEDTEXT +30 <EVAL (<DLOCAL.items>*35)+80> 90 25 <LOCAL.TEXTHUE> <SERV.ITEMDEF.<LOCAL.ITEM>.NAME>
				TILEPICHUE +150 <EVAL (<DLOCAL.items>*35)+80> <SERV.ITEMDEF.<lDB.ROW.<DLOCAL.items>.baseid>.ID> <DSERV.ITEMDEF.<lDB.ROW.<DLOCAL.items>.baseid>.RESDISPDNHUE>
				BUTTON +240 <EVAL (<DLOCAL.items>*35)+80> 0845 0846 1 0 <EVAL <lDB.ROW.<DLOCAL.items>.id>+10000>
			ENDIF			
		ENDFOR
	ENDIF
	DTEXT +10 +365 0 Sayfa: <EVAL (<SRC.CTAG.Craft_Menu.INDEX>/9)+1> 
	IF (<EVAL (<DLOCAL.ITEMCOUNT>/9)>)
		BUTTON +250 +365 5601 5605 1 0 6 // ++ 
	ENDIF
	BUTTON +230 +365 5603 5607 1 0 7 // -- 
	DTEXT +0 +0 0 İçerik (<DLOCAL.ITEMCOUNT>) ;
	
	

ENDIF
lDB.CLOSE


[DIALOG d_craft_menu BUTTON]
// Tekraryap
ON=1

// Bölüm: Page ++
ON=2
CTAG.Craft_Menu.Cat_INDEX +=9
DIALOGCLOSE d_craft_menu
DIALOG d_craft_menu

// Bölüm: Page --
ON=3
IF (<CTAG.Craft_Menu.Cat_INDEX> > 0)
	CTAG.Craft_Menu.Cat_INDEX -=9
ENDIF
DIALOGCLOSE d_craft_menu
DIALOG d_craft_menu


// Alt Bölüm: Page ++
ON=4
CTAG.Craft_Menu.Sub_INDEX +=9
DIALOGCLOSE d_craft_menu
DIALOG d_craft_menu


// Alt Bölüm: Page --
ON=5
IF (<CTAG.Craft_Menu.Sub_INDEX> > 0)
	CTAG.Craft_Menu.Sub_INDEX -=9
ENDIF
DIALOGCLOSE d_craft_menu
DIALOG d_craft_menu



// Eşyalar: Page ++
ON=6
CTAG.Craft_Menu.INDEX +=9
DIALOGCLOSE d_craft_menu
DIALOG d_craft_menu

// Eşyalar: Page --
ON=7
IF (<CTAG.Craft_Menu.INDEX> > 0)
	CTAG.Craft_Menu.INDEX -=9
ENDIF
DIALOGCLOSE d_craft_menu
DIALOG d_craft_menu



// Kategori: Seç
ON=100 199
LDB.CONNECT "db/craftdialog.db"
IF (<LDB.CONNECTED>)
	lDB.QUERY "SELECT id, category FROM '<CTAG.Craft_Menu.Skill>' WHERE id='<eval <DARGN>-100>'"
	IF (<lDB.ROW.NUMROWS>)
		SRC.SYSMESSAGE <ldb.row.0.category>
		CTAG.Craft_Menu.Sub_WHERE=category="<ldb.row.0.category>"
		CTAG.Craft_Menu.Sub_INDEX=0
	
		DIALOGCLOSE d_craft_menu
		DIALOG d_craft_menu
	ENDIF		
ENDIF
lDB.CLOSE


// Alt Kategori: Seç
ON=200 299
LDB.CONNECT "db/craftdialog.db"
IF (<LDB.CONNECTED>)
	lDB.QUERY "SELECT id, category, subsection FROM '<CTAG.Craft_Menu.Skill>' WHERE id='<eval <DARGN>-200>'"
	IF (<lDB.ROW.NUMROWS>)
		SRC.SYSMESSAGE <ldb.row.0.category>
		SRC.SYSMESSAGE <ldb.row.0.subsection>
		
		CTAG.Craft_Menu.WHERE=category="<ldb.row.0.category>" AND subsection="<ldb.row.0.subsection>"
		
		CTAG.Craft_Menu.INDEX=0
		CTAG.Craft_Menu.ORDER="name ASC"
		DIALOGCLOSE d_craft_menu
		DIALOG d_craft_menu
		
	ENDIF		
ENDIF
lDB.CLOSE



// Eşya üret
ON=1000 9999
LDB.CONNECT "db/craftdialog.db"
IF (<LDB.CONNECTED>)
	lDB.QUERY "SELECT * FROM '<CTAG.Craft_Menu.Skill>' WHERE id='<eval <DARGN>-1000>'"
	IF (<lDB.ROW.NUMROWS>)
		LOCAL.ITEM <ldb.row.0.baseid>
	ENDIF		
ENDIF
lDB.CLOSE

IF !(<SERV.ITEMDEF.<LOCAL.ITEM>>)
	SRC.CTAG.Craft_Menu.Notice <DEF.BFONT_DRED>Bir hata oluştu. Bu eşya sunucuda kayıtlı değil.
	DIALOGCLOSE d_craft_menu
	DIALOG d_craft_menu
	RETURN 1
ELSEIF !(<CANMAKESKILL <LOCAL.ITEM>>)
	SRC.CTAG.Craft_Menu.Notice <DEF.BFONT_DRED>Gerekli beceri : <SERV.ITEMDEF.<LOCAL.ITEM>.SKILLMAKE>
	DIALOGCLOSE d_craft_menu
	DIALOG d_craft_menu
	RETURN 1
ELSEIF !(<CANMAKE <LOCAL.ITEM>>)
	SRC.CTAG.Craft_Menu.Notice <DEF.BFONT_DRED>Gerekli malzeme : <SERV.ITEMDEF.<LOCAL.ITEM>.RESMAKE>
	DIALOGCLOSE d_craft_menu
	DIALOG d_craft_menu
	RETURN 1
ENDIF
IF (<ISONLINE>)

	MAKEITEM <LOCAL.ITEM>
	TAG0.CRAFT_LASTMAKE_BASEID <LOCAL.ITEM>
	SYSMESSAGE @044,,1,1 <SERV.ITEMDEF.<TAG0.CRAFT_LASTMAKE_BASEID>.NAME> isimli eşyayı üretmeye başladın.
	SRC.CTAG.Craft_Menu.Notice <DEF.BFONT_GREEN><SERV.ITEMDEF.<LOCAL.ITEM>.NAME> isimli eşyayı üretmeye başladın.
	
	DIALOGCLOSE d_craft_menu
	DIALOG d_craft_menu
ENDIF




// Eşya incele
ON=10000 19999
LDB.CONNECT "db/craftdialog.db"
IF (<LDB.CONNECTED>)
	lDB.QUERY "SELECT * FROM '<CTAG.Craft_Menu.Skill>' WHERE id='<eval <DARGN>-10000>'"
	IF (<lDB.ROW.NUMROWS>)
		LOCAL.ITEM <ldb.row.0.baseid>
		SRC.CTAG0.CRAFT_MENU_ITEM_PROPS <LOCAL.ITEM>
		// DIALOGCLOSE d_craft_menu 
		// DIALOG d_craft_menu 
	ENDIF		
ENDIF
lDB.CLOSE





[eof]